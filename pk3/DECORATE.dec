actor SamsaraChangeFlash
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Height 0
    Radius 0

    ReactionTime 35

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 AAAAAA 0 A_SpawnItemEx("ChangeSparkle", random(-240, 240)/10.0,0,random(-40, 40)/10.0, random(-10, 10)/40.0, 0, random(20, 40)/40.0, random(0, 360))
        TNT1 A 1 A_Countdown
        loop

      Death:
        TNT1 A 0
        stop
    }
}

actor ChangeSparkle
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    Scale 0.3
    Alpha 0.1

    States
    {
      Spawn:
        WPFG AB 3 bright ThrustThingZ(0, random(0, 8) / 10.0, 0, 1)
        WPFG C 4 bright ThrustThingZ(0, random(0, 8) / 10.0, 0, 1)
        WPFG D 6 bright ThrustThingZ(0, random(0, 8) / 10.0, 0, 1)
        WPFG E 6 bright ThrustThingZ(0, random(0, 8) / 10.0, 0, 1)
        WPFG F 4 bright ThrustThingZ(0, random(0, 8) / 10.0, 0, 1)
        WPFG GH 3 bright
        stop
    }
}

actor SamsaraSlotPickup: CustomInventory
{
    +QUIET
    Inventory.PickupMessage ""

    States
    {
      Spawn:
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(215, 1) == 1, "IWasDropped")
        goto SpawnLoop

      SpawnLoop:      
        "----" A 35 ACS_ExecuteAlways(228)
        loop

      PickupSuccess:
        TNT1 A 0 A_SetBlend("00 00 00", 0, 0)
        stop

      PickupFailure:
        TNT1 A 0 A_SetBlend("00 00 00", 0, 0)
        fail
    }
}


actor SamsaraSlotOnePickup: SamsaraSlotPickup
{
    States
    {
      IWasDropped:
        TNT1 A 0 A_SpawnItemEx("SamsaraSlotOneDrop", 0,0,0, momx,momy,momz, 0, SXF_ABSOLUTEMOMENTUM)
        stop

        // The A_FadeIns are here just so I can do one-liners
      NoGuy:    WEPG A 35 A_FadeIn(0)   goto SpawnLoop

      Doomguy:  WSAW A 35 A_FadeIn(0)   goto SpawnLoop
      Chexguy:  CSPR A 35 A_FadeIn(0)   goto SpawnLoop
      Heretic:  WGNT A 35 A_FadeIn(0)   goto SpawnLoop
      Wolfguy:  WKNP A 35 A_FadeIn(0)   goto SpawnLoop
      Hexen:    PSBG A 35 A_FadeIn(0)   goto SpawnLoop
      Dukeguy:  WPBM A 35 A_FadeIn(0)   goto SpawnLoop
      Marathon: FSMG A 35 A_FadeIn(0)   goto SpawnLoop

      DoomHybrid_Full:      PSAW A 35 A_FadeIn(0)   goto SpawnLoop
      ChexHybrid_Full:      PSAW A 35 A_FadeIn(0)   goto SpawnLoop
      HereticHybrid_Full:   PGNT A 35 A_FadeIn(0)   goto SpawnLoop
      WolfHybrid_Full:      PKNF A 35 A_FadeIn(0)   goto SpawnLoop
      HexenHybrid_Full:     PSAW A 35 A_FadeIn(0)   goto SpawnLoop
      DukeHybrid_Full:      PSAW A 35 A_FadeIn(0)   goto SpawnLoop
      MarathonHybrid_Full:  PSAW A 35 A_FadeIn(0)   goto SpawnLoop

      DoomHybrid_Empty:     PSAW B 35 A_FadeIn(0)   goto SpawnLoop
      ChexHybrid_Empty:     PSAW B 35 A_FadeIn(0)   goto SpawnLoop
      HereticHybrid_Empty:  PGNT B 35 A_FadeIn(0)   goto SpawnLoop
      WolfHybrid_Empty:     PKNF B 35 A_FadeIn(0)   goto SpawnLoop
      HexenHybrid_Empty:    PSAW B 35 A_FadeIn(0)   goto SpawnLoop
      DukeHybrid_Empty:     PSAW B 35 A_FadeIn(0)   goto SpawnLoop
      MarathonHybrid_Empty: PSAW B 35 A_FadeIn(0)   goto SpawnLoop

      Pickup:
        TNT1 A 0 A_JumpIf((ACS_ExecuteWithResult(229, 1) & 3) == 1, "PickupSuccess")
        goto PickupFailure
    }
}

actor SamsaraSlotOneDrop: SamsaraSlotOnePickup
{
    States
    {
      Spawn:
        TNT1 A 0
        goto SpawnLoop

      Pickup:
        TNT1 A 0 A_JumpIf((ACS_ExecuteWithResult(229, 1, 1) & 1) == 1, "PickupSuccess")
        goto PickupFailure
    }
}
