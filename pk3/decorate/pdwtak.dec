actor PeopleDieParticle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    Radius 0
    Height 0

    States
    {
      Spawn:
        TNT1 A 70
        stop
    }
}

actor DoomguysDieParticle: PeopleDieParticle
{
    RenderStyle Add
    Alpha 0.75

    Scale 0.75

    States
    {
      Spawn:
        PLSS AAAAAABBBBBB 1 bright A_FadeOut(0.005)
        loop
    }
}

actor DoomguysDiePulse: PeopleDieParticle
{
    ReactionTime 35

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("DoomguysDieParticle", 0,0,0, random(100,200)/250.0,0,random(-25,25)/250.0, random(0,360))
        TNT1 A 0 A_Countdown
        loop

      Death:
        TNT1 A 0
        stop
    }
}

actor InvPeopleDie: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_FireCustomMissile("PeopleDieWhenTheyAreKilled")
        stop
    }
}

actor InvPeopleZorch: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_FireCustomMissile("PeopleDieWhenTheyAreZorched")
        stop
    }
}

actor PeopleDieWhenTheyAreKilled
{
    +NOINTERACTION
    +FORCERADIUSDMG

    Projectile
    Speed 0

    Radius 8
    Height 8

    Obituary "%k exploded %o."

    States
    {
      Spawn:
      Death:
        TNT1 A 0
        TNT1 A 280 A_Explode(512, 256)
        stop
    }
}

actor PeopleDieWhenTheyAreZorched: PeopleDieWhenTheyAreKilled
{
    DamageType "Zorch"
}

actor DoomguyDiesWhenHeIsKilled: PeopleDieParticle
{
    RenderStyle Add
    Scale 2.5

    States
    {
      Spawn:
        MISL B 0 bright
        MISL B 8 bright A_PlaySound("weapons/rocklx", CHAN_BODY, 1, 0, 0.5)
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}

actor CorvusDiesWhenHeIsKilled: PeopleDieParticle
{
    RenderStyle Add
    Scale 2.5
    
    States
    {
      Spawn:
        FX00 A 0 bright
        FX08 A 6 bright A_PlaySound("weapons/phoenixhit", CHAN_BODY, 1, 0, 0.5)
        FX08 BC 5 bright
        FX08 DEFGH 4 bright
        Stop
    }
}

actor ChexguyDiesWhenHeIsKilled: PeopleDieParticle
{
    RenderStyle Add
    Scale 1.875

    Alpha 0.75
    
    States
    {
      Spawn:
        TNT1 A 0 bright
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ChexBFGTrail",0,0,0,random(-2,2),random(-2,2),random(-2,2),random(-180,180),32)//A_CustomMissile("ChexBFGTrail",0,0,random(-180,180),2,random(-180,180))
        TNT1 A 0 bright A_PlaySound("chex/bfgx", CHAN_BODY, 1, 0, 0.5)
        TNT1 A 0 A_SpawnItemEx("ChexBFGExplosion")
        CFS3 E 8 bright
        CFS3 EEEEDDDDCCCCBBBBAAAA 2 bright A_FadeOut(0.04)
        CFS3 A 1 bright A_FadeOut(0.01)
        wait
    }
}

actor BlazkoDiesWhenHeIsKilled: PeopleDieParticle
{
    RenderStyle Add
    Scale 2
    Alpha 0.65

    States
    {
      Spawn:
        SPX1 A 0
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("FakeSpearPopcorn", 0,0,0, random(-25,25)/2.5,0,random(-25,25)/2.5, random(0, 360))
        SPX1 A 0 A_PlaySound("wolfen/spearexplode1")
        SPX1 A 4 BRIGHT A_Explode(120, 240, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor FakeSpearPopcorn
{
    Radius 3
    Height 4

    +NOINTERACTION
    +CLIENTSIDEONLY

    DamageType "BJSpear"

    SeeSound "wolfen/spearbounce"
    DeathSound "wolfen/spearexplode2"

    Alpha 0.5
    Scale 0.75

    States
    {
      Spawn:
        SPB2 ABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(128, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX2 ABCDE 4 BRIGHT
        Stop
    }
}

