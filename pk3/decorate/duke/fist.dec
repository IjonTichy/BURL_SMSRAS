actor DukeKicking : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 }

actor "Mighty Boot" : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    states
    {
      Select:
        TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt1")
        TNT1 A 1 A_Raise
        TNT1 A 0 A_Raise
        TNT1 A 0 A_Raise
        loop

      Deselect:
        TNT1 A 1 A_Lower
        TNT1 A 0 A_Lower
        TNT1 A 0 A_Lower
        loop

      Ready:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SynthFireActive",1)
        // Left Boot
      LIdle:
        TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt")
        TNT1 A 0 A_JumpIfInventory("DukeWaiting",15,"WaitingTaunt")
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        TNT1 A 1 A_WeaponReady(12)
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        Loop

      LFire:
        TNT1 A 0 A_GiveInventory("DukeKicking",1)
        DKBT A 5 A_WeaponReady(14)
        TNT1 A 0 A_FireCustomMissile("DukeKickAttack")
        DKBT B 7 A_WeaponReady(14)
        DKBT A 3 A_WeaponReady(14)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_TakeInventory("DukeKicking",9)
        DKBT A 1 A_WeaponReady(14)
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        Goto LIdle
        // Right Boot
      Flash:
        TNT1 A 1

      RIdle:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        Loop

      RFire:
        TNT1 A 0 A_GiveInventory("DukeKicking",1)
        TNT1 A 0 ACS_ExecuteAlways(212,0,3)
        DKBT C 6 A_WeaponReady(14)
        TNT1 A 0 A_FireCustomMissile("DukeKickAttack")
        DKBT D 9 A_WeaponReady(14)
        DKBT C 3 A_WeaponReady(14)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_TakeInventory("DukeKicking",9)
        DKBT C 1 A_WeaponReady(14)
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        Goto RIdle
        
      IntroTaunt:
      IntroTaunt1:
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,"IntroTaunt2")
        KNUX A 3
        KNUX B 3
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,"IntroTaunt3")
        TNT1 A 0 A_PlaySoundEX("duke/introtaunt","SoundSlot7",0,0)
        KNUX C 3
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown",5)
        TNT1 A 0 ACS_ExecuteAlways(205)
        KNUX D 3 A_PlayWeaponSound("duke/knuckles")
        KNUX C 3
        KNUX B 3
        KNUX A 3 A_TakeInventory("DukeReady",1)
        //KNUX AAAAAAAAAAAAAA 0 A_Lower
        goto Select

      IntroTaunt2:
        KNUX A 3
        KNUX B 3

      IntroTaunt3:
        KNUX C 3
        KNUX D 3 A_PlayWeaponSound("duke/knuckles")
        KNUX C 3
        KNUX B 3
        KNUX A 3 A_TakeInventory("DukeReady",1)
        //KNUX AAAAAAAAAAAAAA 0 A_Lower
        goto Select
        
      WaitingTaunt:
      WaitingTaunt1:
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,"IntroTaunt2")
        KNUX A 3
        KNUX B 3
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,"IntroTaunt3")
        TNT1 A 0 A_PlaySoundEX("duke/waitingtoolong","SoundSlot7",0,0)
        KNUX C 3
        TNT1 A 0 A_GiveInventory("DukeTauntCooldown",5)
        TNT1 A 0 ACS_ExecuteAlways(205)
        KNUX D 3 A_PlayWeaponSound("duke/knuckles")
        KNUX C 3
        KNUX B 3
        KNUX A 3 A_TakeInventory("DukeWaiting",15)
        //KNUX AAAAAAAAAAAAAA 0 A_Lower
        goto Select

      WaitingTaunt2:
        KNUX A 3
        KNUX B 3

      WaitingTaunt3:
        KNUX C 3
        KNUX D 3 A_PlayWeaponSound("duke/knuckles")
        KNUX C 3
        KNUX B 3
        KNUX A 3 A_TakeInventory("DukeWaiting",15)
        //KNUX AAAAAAAAAAAAAA 0 A_Lower
        goto Select
        
      Fire:
        TNT1 A 0
        goto Ready

      AltFire:
        TNT1 A 0
        goto Ready
    }
}

actor DukeBootPuff : BulletPuff
{
    Radius 1
    Height 1
    Alpha 1
    Scale 0.45
    VSpeed 1.15
    Speed 0
    +MISSILE
    -SOLID
    +NOCLIP
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        //TNT1 A 1 A_PlaySound("doomguy/wallpunch2")
        DKPF ABCD 4
        Stop
    }
}

actor DukeBulletPuff2 : BulletPuff
{
    Alpha 1
    DamageType "DukeDamage"
    Scale 0.55
    VSpeed 0
    -SOLID
    States
    {
      Spawn:
        TNT1 A 0 A_Jump(224,2)
        TNT1 A 0 A_PlaySound("duke/bulletricochet")
        TNT1 A 1 A_SpawnItem("DukeBootPuff")
        DKPF EFGH 3 BRIGHT
        Stop
    }
}

actor DukeKickAttack : FastProjectile
{
    Radius 6
    Height 6
    DamageType "MightyBoot"
    Projectile
    Damage (10)
    Speed 25
    Obituary "%k \cfput\c- %o \cfout with a snap kick.\c-"
    SeeSound "none"
    DeathSound "none"
    Decal "none"
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 1
        Stop

      Death:
      Crash:
        TNT1 A 1 A_Stop
        TNT1 A 0 A_PlaySoundEx("doomguy/wallpunch2","body",0,0)
        TNT1 A 1 A_SpawnItem("DukeBootPuff")
        Stop

      XDeath:
        TNT1 A 1 A_Stop
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        stop
    }
}

actor DukeBootserk: PowerDamage
{
    +HUBPOWER
    Powerup.Duration 0x7FFFFFFF
    DamageFactor "MightyBoot", 8
}
