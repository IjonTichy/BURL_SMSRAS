actor Cacodemon2 : Cacodemon replaces Cacodemon
{
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    BloodType "CacoBlood"
    BloodColor blue
    +NOTDMATCH
    +NOICEDEATH
    states
    {
      Death.DukeExplosive:
      Death.DukeSpectral:

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
	HEAD H 1 A_Scream
	HEAD H 0 A_NoBlocking
	HEAD H 0 A_SpawnItemEx("Cacoballoon",0,0,random(25,25)/100,0,0,0,0,32)
	Stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        HEAD F 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        HEAD F 16 A_Pain
        goto See
		
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        HEAD F 5 A_FaceTarget
		TNT1 A 0 A_Gravity
        HEAD F 30 A_Pain
        HEAD F 70
        Goto See

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Missile"

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoGravity
        HEAD F 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        HEAD F 4 A_SpawnItem("ZorchEffectBig")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        HEAD F 4 A_SetTranslucent(0.9,0)
        HEAD F 0 A_Scream
        HEAD F 4 A_SetTranslucent(0.8,0)
        HEAD F 4 A_SetTranslucent(0.7,0)
        HEAD F 4 A_SetTranslucent(0.6,0)
        HEAD F 4 A_SetTranslucent(0.5,0)
        HEAD F 4 A_SetTranslucent(0.4,0)
        HEAD F 4 A_SetTranslucent(0.3,0)
        HEAD F 4 A_SetTranslucent(0.2,0)
        HEAD F 4 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A 0 A_Gravity
        TNT1 A -1 A_NoBlocking
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        HEAD L 8 A_UnSetFloorClip
        HEAD KJIHG 8
        goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_SetTranslucent(0.1,0)
        HEAD A 3 A_SetTranslucent(0.2,0)
        HEAD A 3 A_SetTranslucent(0.3,0)
        HEAD A 3 A_SetTranslucent(0.4,0)
        HEAD A 3 A_SetTranslucent(0.5,0)
        HEAD A 3 A_SetTranslucent(0.6,0)
        HEAD A 3 A_SetTranslucent(0.7,0)
        HEAD A 3 A_SetTranslucent(0.8,0)
        HEAD A 3 A_SetTranslucent(0.9,0)
        HEAD A 3 A_SetTranslucent(1,0)
        HEAD A 1 A_SetShootable
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_SetFloorClip
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("CacodemonFrozen")
        HEAD E 0 A_GenericFreezeDeath
        HEAD E 70 A_UnsetSolid
        HEAD E 0 A_IceGuyDie
        HEAD E 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}

actor CacodemonBall2 : CacodemonBall replaces CacodemonBall
{
    DamageType "DoomCacodemon"
    +THRUGHOST
	damage (random(5, 10))
	speed 15
	fastspeed 30
}

Actor Cacoballoon : Cacodemon2 {
	SeeSound ""
	DeathSound ""
	bouncefactor 0.85
	//[Bloax] Oh hacks, how I love you.
	-COUNTKILL
	-SOLID
	+DOOMBOUNCE
	States {
  Raise:
  Pain:
  See:
  Spawn:
	HEAD G 0
	HEAD G 1 DamageThing(500000)
	goto Death
  Death: //jpalomo was here
   HEAD G 0 A_PlaySound ("caco/deflate",CHAN_VOICE)
   HEAD G 0 A_NoGravity
   HEAD GGGGGGGG 1 A_ChangeVelocity (random(-6, 6), random(-6, 6), random(0,2), 0)
   HEAD GGGGGGGG 1 A_ChangeVelocity (random(-5, 5), random(-5, 5), random(-2,3), 0)
   TNT1 AAAA 0 A_SpawnItemEx("CacoBlood",random(-4,4),random(-6,6),random(-6,25),momx*random(1,7)*0.1,momy*random(1,7)*0.1,momz*random(1,7)*0.1,0,0,90)
    HEAD IIIIIIIIJJJJJJJJ 0 A_SpawnItemEx("CacoBlood",random(0,4),random(-12,12),random(-6,25),momx*random(1,7)*0.1,momy*random(1,7)*0.1,momz*random(1,7)*0.1,0,0,150)
   HEAD GGGGHHHH 1 A_ChangeVelocity (random(-4, 4), random(-4, 4), random(-2,4), 0)
   HEAD HHHHHHHH 1 A_ChangeVelocity (random(-4, 4), random(-4, 4), random(-3,4), 0)
   HEAD K 0 A_NoBlocking
    HEAD HHHHHHHH 1 A_ChangeVelocity (random(-2, 2), random(-2, 2), random(-2,2), 0)
   TNT1 AAAA 0 A_SpawnItemEx("CacoBlood",random(-4,4),random(-6,6),random(-6,25),momx*random(1,7)*0.1,momy*random(1,7)*0.1,momz*random(1,7)*0.1,0,0,90)
    HEAD K 0 A_SetFloorClip
   HEAD I 0 A_ChangeFlag("DOOMBOUNCE",0)
   HEAD I 1 A_Gravity
    Wait
   Crash:
   HEAD I 0 A_StopSound(CHAN_VOICE)
   HEAD I 1 A_Gravity
   HEAD I 5 A_PlaySound ("caco/crash")
   TNT1 AAAA 0 A_SpawnItemEx("CacoBlood",random(-4,4),random(-6,6),random(-6,25),momx*random(1,7)*0.1,momy*random(1,7)*0.1,momz*random(1,7)*0.1,0,0,90)
   HEAD JK 8
   HEAD L -1
   Stop
}
}

