//=================
// DOOMGUY: Imp
// The only ally with renamed sprites/sounds, so as not to conflict with mods that replace the Imp sprites/sounds.
// Which is actually kind of depressing, if you're a fan of the other iwads. None of them have stuff like that.

actor FriendlyDoomImp : DoomImp2
{
    Health 100
    SeeSound "friendlyimp/sight"
    PainSound "friendlyimp/pain"
    DeathSound "friendlyimp/death"
    ActiveSound "friendlyimp/active"
    HitObituary "%o was somehow killed by a friendly Imp."
    Obituary "%o was somehow killed by a friendly Imp."

    Species "Player"

    -SOLID
    +FRIENDLY

    States
    {
      Spawn:
        TRO2 AB 10 A_Look
        loop

      See:
        TRO2 AABBCCDD 3 A_Chase
        Loop

      Melee:
      Missile:
        TRO2 EF 8 A_FaceTarget
        TRO2 G 6 A_CustomComboAttack("FriendlyDoomImpBall", 32, 3 * random(1, 8), "friendlyimp/melee")//A_TroopAttack
        Goto See

      Pain:
        TRO2 H 2
        TRO2 H 2 A_Pain
        Goto See

      Death:
        TRO2 I 8
        TRO2 J 8 A_Scream
        TRO2 K 6
        TRO2 L 6 A_NoBlocking
        TRO2 M -1
        Stop

      XDeath:
        TRO2 N 5
        TRO2 O 5 A_XScream
        TRO2 P 5
        TRO2 Q 5 A_NoBlocking
        TRO2 RST 5
        TRO2 U -1
        Stop

      Raise:
        stop
    }
}

actor FriendlyDoomImpBall : DoomImpBall2
{
    +THRUSPECIES

    Species "Player"

    SeeSound "friendlyimp/attack"
    DeathSound "friendlyimp/shotx"

    States
    {
      Spawn:
        DBL1 AB 4
        Loop
        
      Death:
        DBL1 CDE 6
        Stop
    }
}

//=================
// CORVUS: Nitrogolem
// The best name ever. Suck it, Gez!
actor FriendlyNitrogolem : MummyLeader2
{
    Health 100
    HitObituary "%o was somehow killed by a friendly Nitrogolem."
    Obituary "%o was somehow killed by a friendly Nitrogolem."

    -SOLID
    +FRIENDLY
    Species "Player"

    States
    {
      Missile:
        MUMM X 5 A_FaceTarget
        MUMM Y 5 Bright A_FaceTarget
        MUMM X 5 A_FaceTarget
        //MUMM Y 5 Bright A_FaceTarget - So as not to be so damned useless.
        //MUMM X 5 A_FaceTarget
        MUMM Y 5 Bright A_CustomComboAttack("FriendlyNitrogolemShot", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
        Goto See
    }
}

actor FriendlyNitrogolemShot : MummyFX1
{
    +THRUSPECIES
    Species "Player"
}

//=================
// CHEX WARRIOR: Bipedal Flemoid.
// Oddly enough, never shot balls of slime despite having more orifices to shoot from than the armored one.
actor FriendlyFlemoid : FlemoidusBipedicus2
{
    Health 100
    SeeSound "friendlyflemoid/sight"
    AttackSound "friendlyflemoid/attack"
    PainSound "friendlyflemoid/pain"
    DeathSound "friendlyflemoid/death"
    ActiveSound "friendlyflemoid/active"

    -SOLID
    +FRIENDLY
    Species "Player"

    Obituary "%o was somehow slimed by a friendly flemoid."

    States
    {
      Spawn:
        BPFL AB 10 A_Look
        Loop

      See:
        BPFL AABBCCDD 3 A_Chase
        Loop

      Pain:
        BPFL A 3 A_SpawnDebris("FlemoidGib",1)
        BPFL A 3 A_Pain
        Goto See

      Raise:
        stop
        
      Pain.ZorchPistol:
        
      Pain.Zorch:
        BPFL G 3
        BPFL G 3 A_Pain
        Goto See
        
      Death.DukeExplosive:
        
      Death.DukeDamage:
        
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death
        
      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death
        
      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        SPOD A 10
        SPOD ABC 7
        SPOD D -1
        stop
        
      Death.ZorchPistol:
        
      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        BPFL H 5
        BPFL I 5 A_Scream
        BPFL J 5 A_NoBlocking
        BPFL K 5
        BPFL L -1
        Stop
        
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice
        
      Ice:
      Death.HexenIce:
        
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        BPFL A 0 A_UnsetSolid
        BPFL A 70 A_GenericFreezeDeath
        BPFL A 0 A_IceGuyDie
        BPFL A 1 A_IceGuyDie
        TNT1 A -1
        stop
        
      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        BPFL A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        BPFL A 16 A_Pain
        Goto See
        
      Melee:
      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        BPFL EF 6 A_FaceTarget // Previously 8 each
        BPFL E 6 A_CustomComboAttack("FriendlyFlemBall", 32, random[troopattack](1,8)*3, "friendlyflemoid/shotx", "flem")
        Goto See
    }
}

actor FriendlyFlemBall : BipedicusBallV3
{
    +THRUSPECIES
    Species "Player"
    SeeSound "friendlyflemoid/flem"
    DeathSound "friendlyflemoid/shotx"
    States
    {
      Spawn:
        FBL1 AB 4
        Loop
        
      Death:
        FBL1 CDE 6
        Stop
    }
}

//=================
// B.J. BLAZKOWICZ: Nazi Guard
// Pretty sure "friendly Nazi" is an oxymoron. How many people are gonna get on our asses for having a Nazi on the heroic side?
ACTOR FriendlyNazi
{
    Health 100
    Radius 16
    Height 56
    Speed 8
    PainChance 170
    Scale 1.25
    Monster

    +FLOORCLIP

    SeeSound "friendlynazi/sight"
    PainSound "friendlynazi/pain"
    DeathSound "friendlynazi/death"
    ActiveSound "friendlynazi/active"
    AttackSound "friendlynazi/attack"

    Obituary "%o was somehow killed by a friendly Nazi."
    Dropitem "Clip"

    Species "Player"
    +FRIENDLY
    -SOLID

    States
    {
      Spawn:
        NGRD A 10 A_Look
        Loop

      See:
        NGRD BBCCDDEE 3 A_Chase
        Loop

      Missile:
        NGRD H 5 A_FaceTarget
        NGRD H 5 A_FaceTarget
        NGRD I 5 Bright A_CustomMissile("FriendlyNaziBullet",32,0,0,8)
        //NGRD H 1 A_CPosRefire
        Goto See

      Pain:
        NGRD F 3
        NGRD F 3 A_Pain
        Goto See

      Death:
        NGRD I 5
        NGRD J 5 A_Scream
        NGRD K 5 A_NoBlocking
        NGRD L 5
        NGRD M -1
        Stop
    }
}

actor FriendlyNaziBullet : WolfenBullet2
{
    Damage (Random(1,3)*5)
    +THRUSPECIES
    Species "Player"
}

//=================
// PARIAS: Afrit
// Squawk.
actor FriendlyAfrit : FireDemon2
{
    Health 100

    Species "Player"
    -SOLID
    +FRIENDLY
    -INVULNERABLE

    States
    {
      Spawn:
      See:
        FDMN ABC 5 Bright A_FireDChase
        Loop

      Missile:
        FDMN K 3 Bright A_FaceTarget
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        Goto Chase
    }
}

actor FriendlyFireDemonMissile : FireDemonMissile
{
    +THRUSPECIES
    Species "Player"
}

//=================
// DUKE NUKEM: Alien Trooper.
// I don't have anything witty to say here.
//=================
// SECURITY OFFICER: Phfor Trooper.
// These guys are probably the closest to the actual Rebels.
//=================
// RANGER: Grunt.
// Puppy gonna grunt. Grunt grunt grunt.
