ACTOR PeasantB : Peasant replaces Peasant
{
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255

    +NOTDMATCH
    +NOICEDEATH

    States
    {
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        PEAS O 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        PEAS O 16 A_Pain
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        PEAS O 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        PEAS O 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        PEAS O 3 A_SetTranslucent(0.9,0)
        PEAS O 0 A_Scream
        PEAS O 3 A_SetTranslucent(0.8,0)
        PEAS O 0 A_NoBlocking
        PEAS O 3 A_SetTranslucent(0.7,0)
        PEAS O 3 A_SetTranslucent(0.6,0)
        PEAS O 3 A_SetTranslucent(0.5,0)
        PEAS O 3 A_SetTranslucent(0.4,0)
        PEAS O 3 A_SetTranslucent(0.3,0)
        PEAS O 3 A_SetTranslucent(0.2,0)
        PEAS O 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        //TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("NaziFrozen")
        PEAS O 0 A_GenericFreezeDeath
        PEAS O 70 A_UnsetSolid
        PEAS O 0 A_IceGuyDie
        PEAS O 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}

ACTOR Peasant5B : PeasantB replaces Peasant5 // Derwin. Tear his ear off for the Gubnuh.
{
Speed 7
ConversationID 10
Translation "128:143=64:79", "48:63=16:31"
DropItem "Ear"
}

ACTOR Peasant7B : PeasantB replaces Peasant7 // Warden Montag. Kill him in the Prison for a prison ID.
{
ConversationID 12
Translation "128:143=24:31", "48:63=216:223"
DropItem "PrisonKey"
}

ACTOR Peasant8B : PeasantB replaces Peasant8 // Da Judge. Kill him to free prisoners.
{
ConversationID 13
Translation "128:143=24:31", "48:63=216:223"
DropItem "SeveredHand"
}

ACTOR Peasant9B : PeasantB replaces Peasant9 // Beldin. Kill him, take his ring to be a part of the Rebellion.
{
ConversationID 14
Translation "128:143=16:31", "48:63=128:143"
DropItem "BeldinsRing"
}