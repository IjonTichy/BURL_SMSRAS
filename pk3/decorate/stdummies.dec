// Weapon/item spawners for Skulltag-specific maps. Wahey!

//Actor InvisibilitySphere : PartialInvisibility { }
actor MaxArmorBonus : ArmorScrap 5091
{
    Game Doom
    SpawnID 167
}

actor MaxHealthBonus : HealPack0 5090
{
    Game Doom
    SpawnID 166
}

// Monster spawners
actor Abaddon : Cacodemon2 5015
{
    Game Doom
    Health 1200
    SpawnID 220
    Speed 12
    PainChance 40
    Translation "170:191=[135,135,135]:[0,0,0]", "16:47=[200,200,200]:[0,0,0]", "164:167=108:108", "217:223=107:109", "112:123=160:167", "124:125=77:78", "126:127=1:2", "192:207=168:187", "240:247=188:191", "48:79=80:111", "144:151=176:191"
    Obituary "%o was smitten by an Abaddon."
    HitObituary "%o became food for an Abaddon."
}

actor Belphegor : BaronOfHell2 5008
{
    Game Doom
    SpawnID 215
    Health 1500
    PainChance 25
    Obituary "%o was slain by a Belphegor."
    HitObituary "%o was slain by a Belphegor."
    Translation "16:47=176:191", "48:79=80:111"
}

actor BloodDemon : DoomDemon2 5004
{
    Game Doom
    SpawnID 156
    Health 300
    Obituary "%o was chomped on by a Blood Demon."
    Translation "16:35=28:47", "36:47=[75,0,0]:[0,0,0]", "160:162=168:168", "163:166=164:167", "224:230=165:165", "161:161=164:164", "80:111=[200,200,200]:[0,0,0]", "208:223=177:188"
}

actor Cacolantern : Cacodemon2 5006
{
    Game Doom
    SpawnID 159
    Health 800
    Obituary "%o was smitten by a Cacolantern."
    HitObituary "%o got too close to a Cacolantern."
    Translation "171:175=209:214", "176:187=212:223", "188:191=232:235", "16:39=212:223", "40:47=232:235", "164:167=215:218"
}

actor DarkImp : DoomImp2 5003
{
    Game Doom
    SpawnID 155
    Health 120
    Obituary "%o was burned by a Dark Imp."
    HitObituary "%o was slashed by a Dark Imp."
    Translation "48:79=[185,185,185]:[0,0,0]", "175:183=197:205", "169:174=194:200", "184:185=206:207", "186:191=240:245", "16:47=[0,0,255]:[0,0,0]", "232:235=201:204", "160:167=198:204", "208:223=193:207"
	States
	{
	Melee:
    Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TROO EF 6 A_FaceTarget
		TROO G 5 A_CustomComboAttack("DarkImpBall",32,3*random(1,8),"imp/melee")//A_CustomMissile("DarkImpBall")
		TNT1 A 0 A_Jump(64,"Missile")
		Goto See
	}
}

Actor DarkImpBall : DoomImpBall
{
Translation "176:183=200:207", "184:191=240:247", "168:175=198:199", "210:223=198:203", "233:233=202:202", "231:231=197:197"
}

actor Hectebus : Fatso2 5007
{
    Game Doom
    SpawnID 158
    Health 1200
    PainChance 20
    Obituary "%o was cremated by a Hectebus."
    Translation "48:63=80:95", "64:79=96:111", "1:2=5:6", "13:15=106:111", "236:239=108:111", "144:151=103:111", "16:47=112:127", "208:223=80:111", "176:191=112:127", "168:175=112:127", "160:167=112:127", "232:236=96:108"
	States
	{
    Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		FATT G 20 A_FatRaise
		FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,16.875,1)
		FATT H 10 Bright
		FATT IG 5 A_FaceTarget
		FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,-16.875,1)
		FATT H 10 Bright
		FATT IG 5 A_FaceTarget
		FATT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,0,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
		FATT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
		FATT H 10 Bright
		FATT IG 5
		goto See
	}
}

actor HectShot : BaronBall//FatShot - Surprisingly, the Baron Ball looks better.
{
Scale 1.5
Translation "176:191=112:127", "208:223=112:123", "168:175=112:115", "231:231=112:112", "32:47=121:121", "160:167=112:127", "232:235=112:127", "61:79=115:124", "128:143=112:127", "16:31=112:115", "144:151=121:127", "13:15=122:122"
}

actor SuperShotgunGuy : ChaingunGuy2 5005
{
    Game Doom
    SpawnID 157
    Health 120
    AttackSound "doomguy/sshotf"
    DropItem "SuperShotgunPickup"
    Obituary "%o was pounded by a Riot Shotgun Guy."
    Translation "64:79=48:79", "30:47=112:127"
    States
    {
      Missile:
        CPOS E 9 A_FaceTarget
        CPOS F 6 Bright A_CustomBulletAttack(8, 7, 7, 5, "BulletPuff")
        CPOS E 6
        Goto See
    }
}

actor SuperShotgunPickup: SamsaraSlotThreeDrop
{
    States
    {
      Spawn:
        SGN2 A -1
        stop
    }
}
