actor Luger: InstantSwitchWeapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Inventory.PickUpSound "wolfen/knifeget"
    Obituary "%k \cjput a hole in\c- %o \cjwith the Luger.\c-"
    Inventory.PickupMessage "You got the Luger Pistol!"
    DamageType "Pistol"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Weapon.YAdjust 16
    States
    {
      Spawn:
        WPSP A -1
        Loop

      Ready:
        WPST F 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        WPST B 3
        WPST F 0 A_PlayWeaponSound("wolfen/pistol")
        WPST C 3 BRIGHT A_FireBullets(0,0,1,7,"BlankPuff2")
        WPST DE 4
        Goto Ready

      CoopFire:
        WPST B 3
        WPST F 0 A_PlayWeaponSound("wolfen/pistol")
        WPST C 3 BRIGHT A_FireCustomMissile("WolfenBullet2",0,0,0,1)
        WPST DE 4
        Goto Ready
    }
}

actor BlankPuff2: BlankPuff { DamageType "Pistol" }

actor WolfenBullet2: FastProjectile
{
    Damage (Random(1,3)*7)
    Radius 1
    Height 1
    Speed 300
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "Pistol"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}
