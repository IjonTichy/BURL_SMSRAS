actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "%k fisted %o with a pair of brass knuckles."
    States
    {
      Ready:
        DPUN A 1 A_WeaponReady
        loop

      Deselect:
        DPUN A 1 A_Lower
        loop

      Select: 
        DPUN A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk",1,"FireBerserk")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom",1,"VanillaFire")//A_JumpIf(ACS_ExecuteWithResult(219,0,0,0)==1,"VanillaFire")
        DPUN LBCD 1
        DPUN E 1 A_CustomPunch(random(1,10)*2,1,0,"DoomPunchPuff")//A_Punch
        DPUN FGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready

      FireBerserk:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom",1,"VanillaFireBerserk")//A_JumpIf(ACS_ExecuteWithResult(219,0,0,0)==1,"VanillaFireBerserk")
        DPUN LBCD 1
        DPUN E 1 A_CustomPunch(random(10,20)*12,1,0,"DoomPunchPuff2") // Technically should be random(2,20)*10, but oh well.
        DPUN FGHI 2
        DPUN JKL 1
        DPUN B 5 A_ReFire
        goto Ready

      VanillaFire:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk",1,"VanillaFireBerserk")
        DPUN M 4
        DPUN N 4 A_CustomPunch(random(1,10)*2,1,0,"DoomPunchPuff")
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready

      VanillaFireBerserk:
        DPUN M 4
        DPUN N 4 A_CustomPunch(random(10,20)*12,1,0,"DoomPunchPuff2")
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready
    }
}

actor DoomPunchPuff : BulletPuff
{
    DamageType "Pistol"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff2 : BulletPuff
{
    DamageType "Pistol"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}
