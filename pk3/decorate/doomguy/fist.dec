actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "%k \cdfisted\c- %o \cdwith a pair of brass knuckles.\c-"
    States
    {
      Ready:
        DPUN A 1 A_WeaponReady
        loop

      Deselect:
        DPUN A 1 A_Lower
        loop

      Select: 
        DPUN A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "Rage")
        DPUN LBCD 1
        goto FireNorm

      Rage:
        DPUN LC 2
        goto FireNorm

      FireNorm:
        DPUN E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DPUN E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff")
        goto FireEnd

      FireBerserk:
        DPUN E 0 A_CustomPunch(random(10, 20)*12, 1, 0, "DoomPunchPuff2") // Technically should be random(1, 10)*20, but oh well.
        goto FireEnd

      FireEnd:
        DPUN E 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageEnd")
        DPUN E 1
        DPUN FGHI 2
        DPUN JKL 1
        DPUN B 5 A_ReFire
        goto Ready

      RageEnd:
        DPUN EFGHIJL 2
        DPUN B 4 A_ReFire
        goto Ready

      VanillaFire:
        DPUN M 4
        goto VanillaFireNorm

      VanillaFireNorm:
        DPUN M 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "VanillaFireBerserk")
        DPUN N 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff")
        goto VanillaEnd

      VanillaFireBerserk:
        DPUN N 0 A_CustomPunch(random(10, 20)*12, 1, 0, "DoomPunchPuff2")
        goto VanillaEnd
    
      VanillaEnd:
        DPUN N 4
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready
    }
}

actor DoomPunchPuff : BulletPuff
{
    DamageType "Pistol"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff2 : BulletPuff
{
    DamageType "Pistol"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}
