Actor "B.F.G. 9000" : BFG9000
{
    Weapon.SlotNumber 7
    DamageType "BFG"
    Obituary "%k showed %o you don't hide from the B.F.G. 9000."
    +INVENTORY.UNDROPPABLE
    States
    {
      Ready:
        DBFG A 1 A_WeaponReady
        loop

      Deselect:
        DBFG A 1 A_Lower
        loop

      Select:
        DBFG A 1 A_Raise
        loop

      Fire:
        DBFG A 0 A_PlayWeaponSound("doomguy/bfgf")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        DBFG A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "RageFire")
        DBFG A 1 Offset(-1, 33)
        DBFG A 1 Offset(0, 33)
        DBFG A 1 Offset(1, 33)
        DBFG A 1 Offset(0, 32)
        DBFG A 1 Offset(-1, 32)
        DBFG A 1 Offset(0, 32)
        DBFG A 1 Offset(1, 31)
        DBFG A 1 Offset(0, 31)
        DBFG A 1 Offset(-1, 31)
        DBFG A 1 Offset(0, 30)
        DBFG A 10 Offset(0, 30)
        DBFG B 10 bright
        goto FireMid

      RageFire:
        DBFG A 2 Offset(-1, 33)
        DBFG A 2 Offset(1, 33)
        DBFG A 2 Offset(-1, 32)
        DBFG A 2 Offset(1, 31)
        DBFG A 2 Offset(-1, 31)
        DBFG A 10 Offset(0, 30)
        DBFG B 10 bright
        goto FireMid

      VanillaFire:
        DBFG A 20
        DBFG B 10 bright
        goto FireMid

      FireMid:
        DBFG B 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim", 1, "NoAimMid")
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall")
        goto FireEnd

      NoAimMid:
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall", 0, 0, 25, 25)
        goto FireEnd

      CoopMid:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim", 1, "NoAimCoop")
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall2")
        goto FireEnd

      NoAimCoop:
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2", 0, 0, 25, 25)
        goto FireEnd
        

      FireEnd:
        DBFG B 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DBFG B 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "RageEnd")
        DBFG A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        DBFG B 1 bright offset(0, 36)
        DBFG B 2 bright offset(0, 40)
        DBFG C 2 bright offset(0, 32)
        DBFG C 5 bright offset(0, 32) A_ZoomFactor(0.995, ZOOM_INSTANT)
        DBFG C 0 A_ZoomFactor(1, ZOOM_INSTANT)
        DBFG C 3 bright offset(0, 26) A_ReFire
        DBFG C 3 bright
        DBFG B 4 bright offset(0, 32)
        DBFG A 10
        goto Ready

      RageEnd:
        DBFG A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        DBFG B 2 bright offset(0, 36)
        DBFG B 2 bright offset(0, 40)
        DBFG C 2 bright offset(0, 32)
        DBFG C 4 bright offset(0, 32) A_ZoomFactor(0.995, ZOOM_INSTANT)
        DBFG C 0 A_ZoomFactor(1, ZOOM_INSTANT)
        DBFG C 4 bright offset(0, 26) A_ReFire
        DBFG C 2 bright A_Refire
        DBFG B 4 bright offset(0, 32) A_Refire
        DBFG A 10 A_Refire
        goto Ready

      VanillaEnd:
        DBFG B 10 bright
        DBFG B 20 A_Refire
        goto Ready

      Flash:
        DBFF A 10 bright A_Light1
        DBFF B 6 bright A_Light2
        Goto LightDone

      Spawn:
        WBFG A -1
        stop
    }
}

actor DoomBFGBall : BFGBall
{
    DamageType "BFG"
    Decal BFGLightning
    +BRIGHT
    +DONTREFLECT
    DeathSound "doomguy/bfgx"
    Obituary "%k obliterated %o with the B.F.G. 9000."
    States
    {
      Spawn:
        DBFS AB 4 bright
        loop
        
      Death:
        DBFE AB 8 bright
        DBFE C 8 bright A_BFGSpray("DoomBFGSplash")
        DBFE DEF 8 bright
        stop
    }
}

actor DoomBFGBall2 : DoomBFGBall { +THRUSPECIES Species "Player" }

actor DoomBFGSplash
{
    +NOBLOCKMAP
    +CLIENTSIDEONLY
    +NOGRAVITY
    RenderStyle Add
    Alpha 0.75
    States
    {
      Spawn:
        DBFP ABCD 8 bright
        stop
    }
}

actor BFGAmmoWaster
{
    Projectile

    States
    {
      Spawn:
        TNT1 A 0
        stop
    }
}
