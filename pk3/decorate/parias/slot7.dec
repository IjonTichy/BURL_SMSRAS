actor Wraithverge : CWeapWraithverge
{
    Weapon.SelectionOrder 3000
    +WEAPON.PRIMARY_USES_BOTH
    +INVENTORY.UNDROPPABLE
    Weapon.AmmoUse1 25
    Weapon.AmmoUse2 25
    Weapon.AmmoGive1 25
    Weapon.AmmoGive2 25
    Weapon.KickBack 150
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoType2 "Cell"

    Weapon.SisterWeapon "Spectral Wraithverge"

    Obituary "%k \cladded\c- %o\cl's soul to the Wraithverge.\c-"
    Inventory.PickupMessage "You picked up the Wraithverge!"
    Inventory.PickupSound "hexen/weaponget"
    States
    {
      Spawn:
        WWRT A -1
        Stop
        
      Ready:
        CHLY A 1 A_WeaponReady
        Loop
        
      Select:
        CHLY A 1 A_Raise
        Loop
        
      Deselect:
        CHLY A 1 A_Lower
        Loop
        
      Fire:
        CHLY AB 1 bright offset(0, 40)
        CHLY CD 2 bright offset(0, 43)
        CHLY E 2 bright offset(0, 45)
        CHLY F 6 bright offset(0, 48) A_CHolyAttack
        CHLY GG 2 bright offset(0, 40) A_CHolyPalette
        CHLY G 2 offset(0, 36) A_CHolyPalette
        goto FireEnd
        
      FireEnd:
        CHLY A 20
        CHLY A 5 A_Refire
        Goto Ready
    }
}

actor "Spectral Wraithverge": Wraithverge
{
    Weapon.AmmoType1 ""
    Weapon.AmmoType2 ""
    Weapon.AmmoUse1  0
    Weapon.AmmoUse2  0
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon "Wraithverge"
    +POWERED_UP

    States
    {
      Fire:
        CHLY AB 1 bright offset(0, 40)
        CHLY CD 2 bright offset(0, 43)
        CHLY E 2 bright offset(0, 45)
        CHLY F 0 A_SetBlend("80 80 FF", 0.4, 0.33)
        CHLY F 0 ACS_ExecuteAlways(317, 0, 8)
        CHLY F 0 A_PlaySound("weapons/sigil", 6)
        CHLY F 0 A_PlaySound("HolySymbolFire", CHAN_WEAPON)
        CHLY F 6 bright offset(0, 48) A_FireCustomMissile("SpectralHolyMissile")
        CHLY G 4 bright offset(0, 40)
        CHLY G 2 offset(0, 36)
        goto FireEnd
    }
}


actor HolySpirit2 : HolySpirit replaces HolySpirit
{
    Obituary "%k \cladded\c- %o\cl's soul to the Wraithverge.\c-"
    Renderstyle Add
    Speed 10
    Alpha 0.7
    +STRIFEDAMAGE
    +BRIGHT
    DamageType "TimonsAxe"
}

actor HolyMissilePuff2: HolyMissilePuff replaces HolyMissilePuff
{
    -NOBLOCKMAP -NOGRAVITY -DROPOFF -NOTELEPORT
    +NOINTERACTION +CLIENTSIDEONLY
}

actor SpectralHolyMissile: HolyMissile
{
    DamageType "Spectral"
    
    States
    {
      Spawn:
        SPIR PPPP 3 bright A_SpawnItemEx("HolyMissilePuff2")
        goto Death
        
      Death:
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0, -67.5, 6)
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0, -22.5, 6)
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0,  22.5, 6)
        SPIR P 1 bright A_CustomMissile("SpectralSpirit", 0,0,  67.5, 6)
        stop
    }
}

actor SpectralSpirit: HolySpirit2
{
    Damage 0
    DamageType "Spectral"
    +FORCERADIUSDMG
    
    States
    {
      Spawn:
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR A 2 A_CHolySeek
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR A 2 A_CHolySeek
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR B 2 A_CHolySeek
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR B 2 A_CHolyCheckScream
        loop

      Death:
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR D 4
        SPIR E 4 A_Scream
        SPIR FGHI 4
        Stop
    }
}
