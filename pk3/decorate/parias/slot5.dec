actor "Hammer of Retribution" : FWeapHammer
{
    Weapon.SelectionOrder 900
    Weapon.SlotNumber 5
    +BLOODSPLATTER
    +WEAPON.AMMO_OPTIONAL
    +WEAPON.MELEEWEAPON
    +INVENTORY.UNDROPPABLE
    Weapon.AmmoUse1 3
    Weapon.AmmoGive1 25
    Weapon.KickBack 150
    Weapon.YAdjust -10
    Weapon.AmmoType1 "Cell"
    Inventory.PickupMessage "You got the Hammer of Retribution!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "%k \clsplintered\c- %o\cl's skull and spine with the Hammer of Retribution.\c-"
    States
    {
      Spawn:
        WFHM A -1
        Stop

      Select:
        FHMR A 1 A_Raise
        Loop

      Deselect:
        FHMR A 1 A_Lower
        Loop

      Ready:
        FHMR A 1 A_WeaponReady
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        FHMR B 6 Offset (5, 0)
        TNT1 A 0 A_JumpIfCloser(96,"HammerMelee")
        TNT1 A 0 A_PlayWeaponSound("FighterHammerMiss")
        FHMR C 3 Offset (5, 0) //A_FHammerAttack
        FHMR D 3 Offset (5, 0)
        FHMR E 2 Offset (5, 0)
        TNT1 A 0 A_JumpIfInventory("Cell",1,"FireHammer")
        FHMR E 10 Offset (5, 150) //A_FHammerThrow
        FHMR A 1 Offset (0, 60)
        FHMR A 1 Offset (0, 55)
        FHMR A 1 Offset (0, 50)
        FHMR A 1 Offset (0, 45)
        FHMR A 1 Offset (0, 40)
        FHMR A 1 Offset (0, 35)
        FHMR A 1
        Goto Ready

      HammerMelee:
        FHMR C 3 Offset (5, 0) A_CustomPunch(random(60,113),1,0,"HammerPuff",96)// A_FHammerAttack - Deals 60-123 damage.
        FHMR D 3 Offset (5, 0)
        FHMR E 2 Offset (5, 0)
        FHMR E 10 Offset (5, 150) //A_FHammerThrow
        FHMR A 1 Offset (0, 60)
        FHMR A 1 Offset (0, 55)
        FHMR A 1 Offset (0, 50)
        FHMR A 1 Offset (0, 45)
        FHMR A 1 Offset (0, 40)
        FHMR A 1 Offset (0, 35)
        FHMR A 1
        Goto Ready

      FireHammer:
        FHMR E 10 Offset (5, 150) A_FireCustomMissile("HammerMissileDM") //A_FHammerThrow
        FHMR A 1 Offset (0, 60)
        FHMR A 1 Offset (0, 55)
        FHMR A 1 Offset (0, 50)
        FHMR A 1 Offset (0, 45)
        FHMR A 1 Offset (0, 40)
        FHMR A 1 Offset (0, 35)
        FHMR A 1
        Goto Ready

      CoopFire:
        FHMR B 6 Offset (5, 0)
        FHMR C 3 Offset (5, 0) A_FHammerAttack
        TNT1 A 0 A_CustomPunch(3,0,0,"BlankPuff4",84)
        FHMR D 3 Offset (5, 0)
        FHMR E 2 Offset (5, 0)
        FHMR E 10 Offset (5, 150) A_FHammerThrow
        FHMR A 1 Offset (0, 60)
        FHMR A 1 Offset (0, 55)
        FHMR A 1 Offset (0, 50)
        FHMR A 1 Offset (0, 45)
        FHMR A 1 Offset (0, 40)
        FHMR A 1 Offset (0, 35)
        FHMR A 1
        Goto Ready
    }
}

actor HammerMissile2 : HammerMissile replaces HammerMissile
{
    Obituary "%k \clthrew\c- %o \clunder the Hammer.\c-"
    Decal Scorch
    Damage 10
    +THRUGHOST
    +FORCERADIUSDMG
    +SKYEXPLODE
    Species "Player"
    +THRUSPECIES
}

actor HammerMissileDM : HammerMissile
{
    Obituary "%k \clthrew\c- %o \clunder the Hammer.\c-"
    Decal Scorch
    Damage 10
    +THRUGHOST
    +SKYEXPLODE
    States
    {
      Death:
        FHFX I 3 Bright A_SetTranslucent(1, 1)
        FHFX J 3 Bright
        FHFX K 3 Bright A_Explode(64, 128, 0)
        FHFX LM 3 Bright
        FHFX N 3
        FHFX OPQR 3 Bright
        Stop
    }
}


