actor "Gauntlet": InstantSwitchWeapon
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 7

    Weapon.SlotNumber 1

    +UNDROPPABLE

    Inventory.Icon "QGNTZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the gauntlet"
    Obituary "%k \csmade\c- %o \cstalk to the hand.\c-"
    
    Weapon.YAdjust 16
    
    States
    {
      Spawn:
        QGNT Z -1
        stop

      Deselect:
        QGNT A 0 A_StopSound(6)
        goto Super::Deselect

      Ready:
        QGNT A 0 A_PlaySound("quakeweps/gauntlethum", 6, 0.2, 1)
        QGNT A 1 A_WeaponReady
        loop

      Fire:
        QGNT A 0 A_SpawnItemEx("GauntletSparks", random(15,17),0,random(30,34), momx,momy,momz, 0, SXF_ABSOLUTEMOMENTUM)
        QGNT A 0 A_PlaySound("quakeweps/gauntlet", 6, 1, 1)
        QGNT D 0 A_CustomPunch(25, 1, 0, "GauntletPuff")
        QGNT D 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageHold")
        QGNT B 1 bright offset(0, 33)
        QGNT C 1 bright offset(0, 34)
        QGNT D 1 bright offset(0, 33)
        QGNT A 1 bright offset(0, 32)
        QGNT G 0 bright A_Refire
        QGNT G 0 A_StopSound(6)
        QGNT B 1 bright A_Refire
        QGNT CDEFGHIJK 1 A_Refire
        goto Ready

      RageHold:
        QGNT B 0 A_Jump(255, "Rage1", "Rage2")

      Rage1:
        QGNT B 2 bright offset(0, 34)
        QGNT C 2 bright offset(0, 32)
        goto RageEnd

      Rage2:
        QGNT D 2 bright offset(0, 33)
        QGNT A 2 bright offset(0, 35)
        goto RageEnd

      RageEnd:
        QGNT B 0 bright A_Refire
        QGNT G 0 A_StopSound(6)
        QGNT D 2 bright A_Refire
        QGNT CEFGHIJK 2 A_Refire
        goto Ready
    }
}

actor GauntletPuff: QuakeBulletPuff
{
    +PUFFONACTORS

    SeeSound "quakeweps/gauntlethit"
    //Decal "BulletChip"

    States
    {
      Melee:
        TNT1 A 1
        stop

      Crash:
        goto Super::Spawn
    }
}

actor GauntletSparks
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    Radius 2
    Height 2

    RenderStyle Add
    Alpha 0.75
    Scale 0.4

    States
    {
      Spawn:
        DPLE ABCDE 2 bright
        stop
    }
}
