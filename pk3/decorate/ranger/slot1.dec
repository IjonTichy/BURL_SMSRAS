actor "Laser Cannon" : InstantSwitchWeapon
{
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 100

    Weapon.AmmoUse 1
    Weapon.AmmoGive 60
    Weapon.AmmoType "Cell"

    Inventory.Pickupmessage "You got the Laser Cannon!"
    Inventory.PickupSound "quakeweps/weaponpickup"

    Scale 0.75

    +INVENTORY.UNDROPPABLE

    States
    {
      Spawn:
        QLSC Z -1
        Stop
        
      Ready:
        QLSC A 1 A_WeaponReady
        Loop
        
      Fire:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("quakeweps/lasershot")
	    TNT1 A 0 A_SetPitch(pitch-.35)
        QLSC B 1 bright A_ZoomFactor(0.9975, ZOOM_INSTANT)
	    TNT1 A 0 A_SetPitch(pitch+.2)
        QLSC B 1 bright A_ZoomFactor(1, ZOOM_INSTANT)
        QLSC A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QLSC A 0 A_JumpIfInventory("SPModeOn",   1, "CoopFire")
        
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt", 0, 1, 3, -2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt", 0, 0, -3, -2)
        goto Fire2

      CoopFire:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop", 0, 1, 3, -2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop", 0, 0, -3, -2)
        goto Fire2

      Fire2:
	    TNT1 A 0 A_SetPitch(pitch+.15)
        QLSC C 2 bright
        QLSC C 0 A_Refire
        Goto Ready
        
      Hold:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("quakeweps/lasershot")
	    TNT1 A 0 A_SetPitch(pitch-.35)
        QLSC D 1 bright A_ZoomFactor(0.9975, ZOOM_INSTANT)
	    TNT1 A 0 A_SetPitch(pitch+.2)
        QLSC D 1 bright A_ZoomFactor(1, ZOOM_INSTANT)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",   1, "CoopHold")
        
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt", 0, 1, 3, -2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt", 0, 0, -3, -2)
        goto Hold2

      CoopHold:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop", 0, 1, 3, -2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop", 0, 0, -3, -2)
        goto Hold2

      Hold2:
	    TNT1 A 0 A_SetPitch(pitch+.15)
        QLSC E 2 bright A_ClearRefire
        goto Ready
        
      Flash:
        TNT1 A 2 A_Light1
        goto LightDone
    }
}

actor LaserCannonBolt
{
    Radius 2
    Height 2

    Speed 50

    Projectile
    Damage (9) //Original was 18 per shot.

    +HEXENBOUNCE
    +BLOODSPLATTER
    BounceCount 3

    Scale 0.55
    SeeSound "quakeweps/laserbounce"

    Obituary "%k \cstazed\c- %o \cswith the Laser Cannon.\c-"

    States
    {
      Spawn:
        LSCL A 1 bright A_Explode(2, 1, 1)
        loop
        
      Death:
        LSCL A 1 bright
        TNT1 A 1 A_SpawnItemEx("QuakeAxePuff")
        stop
        
      Crash:
      XDeath:
        TNT1 A 1
        stop
    }
}

actor LaserCannonBoltCoop : LaserCannonBolt
{
    +THRUSPECIES
    Species "Player"
}
