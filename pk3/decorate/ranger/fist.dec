actor "Axe": InstantSwitchWeapon
{
    Weapon.SelectionOrder 8
    Weapon.Kickback 100

    +NOALERT
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE

    Inventory.Icon "PSTRA0"
    Obituary "%k met %o and became an axe-murderer."

    States
    {
      Ready:
        QAXE A 1 A_WeaponReady
        Loop

      // 18 tics
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound")
        goto AfterSound

      QuadSound:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        goto AfterSound

      AfterSound:
        QAXE A 0 A_PlaySound("quakeweps/axe", CHAN_WEAPON)
        QAXE A 0 A_Jump(128, "Axe2")
        goto Axe

      // 18 tics

      Axe:
        QAXE B 4
        QAXE C 2
        QAXE A 0 A_CustomPunch(20, 1, 0, "QuakeAxePuff")
        QAXE D 2
        QAXE E 4
        goto AxeEnd

      Axe2:
        QAXE F 4
        QAXE G 2
        QAXE A 0 A_CustomPunch(20, 1, 0, "QuakeAxePuff")
        QAXE G 2
        QAXE H 4
        goto AxeEnd

      AxeEnd:
        QAXE A 6
        goto Ready
    }
}

actor ChainsawShells: RandomSpawner replaces Chainsaw
{
    dropitem "QuakeShellBox"
}

actor QuakeAxePuff: QuakePistolPuff
{
	DamageType "Pistol"
    AttackSound "quakeweps/axewall"
}
