actor "Single Shotgun": InstantSwitchWeapon
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 7
    
    +UNDROPPABLE
    
    Inventory.Icon "QSHTZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the shotgun"
    Obituary "%k gave %o a boomstick to chew on."
    
    //Decal BulletChip
    
    States
    {
      Ready:
        QSGN A 1 A_WeaponReady
        Loop
        
      // 17 tics
      Fire:
        QSGN A 0 A_PlaySound("quakeweps/shotgunfire", CHAN_WEAPON)
        QSGN A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QSGN A 0 A_FireBullets(1.5, 1.5, 4, 3, "QuakePistolPuff")
        goto Fire2
        
      SPFire:
        QSGN A 0 A_FireBullets(1.5, 1.5, 12, 1, "QuakePistolPuff")
        goto Fire2
        
      CoopFire:
        QSGN AAAAAA 0 A_FireCustomMissile("QuakeBullet2", random(-150,150)/100.0, 0, 0, 0, 0, random(-150,150)/100.0)
        goto Fire2
        
      Fire2:
        TNT1 A 0 A_ZoomFactor(0.985, ZOOM_INSTANT)
        QSGN B 2 bright // 2
        TNT1 A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        QSGN C 2 bright // 4
        TNT1 A 0 A_ZoomFactor(1, ZOOM_INSTANT)
        QSGN D 3		// 7
        QSGN E 3		// 10
        QSGN F 4		// 14
        QSGN G 4		// 18
        // 18 frames
        QSGN A 0 A_ReFire
        Goto Ready
		
      Spawn:
        QSHT Z -1
        Stop
    }
}
