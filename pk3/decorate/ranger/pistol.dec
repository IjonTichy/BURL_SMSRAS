actor "Single Shotgun": InstantSwitchWeapon
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 7

    +NOAUTOAIM

    Inventory.Icon "QSHTZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the shotgun"
    Obituary "%o chews on %k's boomstick"

    Decal BulletChip

    States
    {
      Ready:
        QSGN A 1 A_WeaponReady
        Loop

      // 17 tics
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound")
        goto AfterSound

      QuadSound:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        goto AfterSound

      AfterSound:
        QSGN A 0 A_PlaySound("quakeweps/shotgunfire", CHAN_WEAPON)
        QSGN A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QSGN A 0 A_FireBullets(1.5, 1.5, 4, 3, "BulletPuff")
        goto Fire2

      SPFire:
        QSGN A 0 A_FireBullets(1.5, 1.5, 12, 1, "BulletPuff")
        goto Fire2

      CoopFire:
        QSGN AAAAAA 0 A_FireCustomMissile("QuakeBullet", random(-150,150)/100.0, 0, 0, 0, 0, random(-150,150)/100.0)
        goto Fire2

      Fire2:
        QSGN B 3 bright
        QSGN C 4 bright
        QSGN D 5
        QSGN E 6

        QSGN A 0 A_ReFire
        Goto Ready

      Spawn:
        QSHT Z -1
        Stop
    }
}
