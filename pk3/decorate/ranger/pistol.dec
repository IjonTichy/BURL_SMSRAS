actor "Single Shotgun": InstantSwitchWeapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobRangeY 0.5
    //Weapon.BobSpeed 1.1
    //Weapon.BobStyle Alpha
    Weapon.Kickback 100
    Weapon.SelectionOrder 7
    
    +UNDROPPABLE
    
    Weapon.SisterWeapon "Spectral SG"
    Inventory.Icon "QSHTZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the shotgun"
    Obituary "%k \csgave\c- %o \csa boomstick to chew on.\c-"
    
    //Decal BulletChip
    
    States
    {
      Ready:
        QSGN A 1 A_WeaponReady
        Loop
        
      Fire:
        QSGN A 0 ACS_ExecuteAlways(312, 0, 2, 3, 11)
        QSGN A 0 A_PlaySound("quakeweps/shotgunfire", CHAN_WEAPON)
        QSGN A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QSGN A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QSGN A 0 A_FireBullets(1.5, 1.5, 4, 3, "QuakePistolPuff")
        goto Fire2
        
      SPFire:
        QSGN A 0 A_FireBullets(1.5, 1.5, 12, 1, "QuakePistolPuff")
        goto Fire2
        
      CoopFire:
        QSGN AAAAAA 0 A_FireCustomMissile("QuakeBullet2", random(-150,150)/100.0, 0, 0, 0, 0, random(-150,150)/100.0)
        goto Fire2
        
      Fire2:
        QSGN B 2 bright
        QSGN C 2 bright
        QSGN D 3
        QSGN E 3
        QSGN F 4
        QSGN G 4
        // 18 frames
        QSGN A 0 A_ReFire
        Goto Ready
        
      Spawn:
        QSHT Z -1
        Stop
    }
}

actor "Spectral SG": "Single Shotgun"
{
    Weapon.SisterWeapon "Single Shotgun"
    +POWERED_UP

    States
    {
      Fire:
        QSGN A 0 ACS_ExecuteAlways(312, 0, 2, 3, 11)
        QSGN A 0 ACS_ExecuteAlways(317, 0, 6)
        QSGN A 0 A_PlaySound("quakeweps/shotgunfire", CHAN_WEAPON)
        QSGN A 0 A_PlaySound("weapons/sigil", 6)
        QSGN AAAAAA 0 A_FireCustomMissile("QuakeBulletSpectral", random(-150,150)/100.0, 0, 0, 0, 0, random(-150,150)/100.0)
        goto Fire2
    }
}
