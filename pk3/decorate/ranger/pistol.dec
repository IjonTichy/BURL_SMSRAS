actor "Single Shotgun": InstantSwitchWeapon
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 7

    +NOAUTOAIM

    Inventory.Icon "QSHTZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the shotgun"
    Obituary "%k gave %o a boomstick to chew on."

    //Decal BulletChip

    States
    {
      Ready:
        QSGN A 1 A_WeaponReady
        Loop

      // 17 tics
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound")
        goto AfterSound

      QuadSound:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        goto AfterSound

      AfterSound:
        QSGN A 0 A_PlaySound("quakeweps/shotgunfire", CHAN_WEAPON)
        QSGN A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QSGN A 0 A_FireBullets(1.5, 1.5, 4, 3, "QuakeBulletPuff")
        goto Fire2

      SPFire:
        QSGN A 0 A_FireBullets(1.5, 1.5, 12, 1, "QuakeBulletPuff")
        goto Fire2

      CoopFire:
        QSGN AAAAAA 0 A_FireCustomMissile("QuakeBullet", random(-150,150)/100.0, 0, 0, 0, 0, random(-150,150)/100.0)
        goto Fire2

      Fire2:
        QSGN B 3 bright
        QSGN C 4 bright
        QSGN D 5
        QSGN E 6

        QSGN A 0 A_ReFire
        Goto Ready

      Spawn:
        QSHT Z -1
        Stop
    }
}

Actor QuakeBulletPuff
{
	+CLIENTSIDEONLY
	+ALLOWPARTICLES
	-SOLID
	+NOCLIP
    States
    {
      Spawn:
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff2",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("QuakeBulletPuff3",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff4",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
		TNT1 A 1
        Stop
    }
}

Actor QuakeBulletPuff2 : BulletPuff
{
    Radius 1
    Height 1
    Alpha 0.9
    Scale 0.15
	Gravity 0.25
	Renderstyle Translucent
    VSpeed 0
    Speed 0
    +MISSILE
    -SOLID
    +NOCLIP
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        QPRT A 2 A_Gravity
		TNT1 A 0 A_Jump(128,"Spawn")
		QPRT AAAAA 1 A_FadeOut(0.2)
        Stop
    }
}

Actor QuakeBulletPuff3 : QuakeBulletPuff2
{
    States
    {
      Spawn:
        QPRT B 2 A_Gravity
		TNT1 A 0 A_Jump(128,"Spawn")
		QPRT BBBBB 1 A_FadeOut(0.2)
        Stop
    }
}

Actor QuakeBulletPuff4 : QuakeBulletPuff2
{
    States
    {
      Spawn:
        QPRT C 2 A_Gravity
		TNT1 A 0 A_Jump(128,"Spawn")
		QPRT CCCCC 1 A_FadeOut(0.2)
        Stop
    }
}