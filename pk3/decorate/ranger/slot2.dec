actor "Quake SSG": Weapon replaces Shotgun
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 6
    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "AmmoShell"

    +NOAUTOAIM

    Inventory.Icon "SGN2A0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the double-barreled shotgun"
    Obituary "%o eats two loads of %k's buckshot"

    Decal BulletChip

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        QDBS A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        QDBS A 1 A_Raise
        Loop

      Ready:
        QDBS A 1 A_WeaponReady
        Loop

      // 25 tics
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadSound")
        goto AfterSound

      QuadSound:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        goto AfterSound

      AfterSound:
        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        QDBS A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QDBS A 0 A_FireBullets(4, 4, 7, 4, "BulletPuff")
        goto Fire2

      SPFire:
        QDBS A 0 A_FireBullets(4, 4, 32, 1, "BulletPuff")
        goto Fire2

      CoopFire:
        QDBS AAAAAAAAAAAAAAA 0 A_FireCustomMissile("QuakeBullet", random(-400,400)/100.0, 0, 0, 0, 0, random(-400,400)/100.0)
        QDBS A 0 A_FireCustomMissile("QuakeBullet", random(-400,400)/100.0, 1, 0, 0, 0, random(-400,400)/100.0)
        goto Fire2

      Fire2:
        QDBS B 4
        QDBS C 4
        TNT1 A 9
        QDBS D 4
        QDBS E 4
        QDBS A 0 A_ReFire
        Goto Ready

      Spawn:
        SGN2 A -1
        Stop
    }
}
