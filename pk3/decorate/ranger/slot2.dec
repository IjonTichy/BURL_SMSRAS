actor "Double Shotgun": InstantSwitchWeapon
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 6
    Weapon.AmmoUse 2
    Weapon.AmmoGive 20
    Weapon.AmmoType "AmmoShell"
    
    +UNDROPPABLE
    
    Inventory.Icon "QDBSZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the double-barreled shotgun"
    Obituary "%k gave %o two loads of buckshot."
    
    //Decal BulletChip
    
    States
    {
      Ready:
        QDBS A 1 A_WeaponReady
        Loop
        
      Fire:
        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        QDBS A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
        QDBS A 0 A_FireBullets(4, 4, 7, 4, "QuakeBulletPuff")
        goto Fire2
        
      SPFire:
        QDBS A 0 A_FireBullets(4, 4, 32, 1, "QuakeBulletPuff")
        goto Fire2
        
      CoopFire:
        QDBS AAAAAAAAAAAAAAA 0 A_FireCustomMissile("QuakeBullet", random(-400,400)/100.0, 0, 0, 0, 0, random(-400,400)/100.0)
        QDBS A 0 A_FireCustomMissile("QuakeBullet", random(-400,400)/100.0, 1, 0, 0, 0, random(-400,400)/100.0)
        goto Fire2
        
        // 25 tics
      Fire2:
        TNT1 A 0 A_ZoomFactor(0.98, ZOOM_INSTANT)
        QDBS B 1 bright
        QDBS C 1 bright
        TNT1 A 0 A_ZoomFactor(0.985, ZOOM_INSTANT)
        QDBS D 2 bright
        TNT1 A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        QDBS E 2
        TNT1 A 0 A_ZoomFactor(1, ZOOM_INSTANT)
		QDBS E 5
        QDBS F 4
        QDBS G 3
        QDBS H 3
        QDBS I 2
        QDBS A 2
        QDBS I 0 A_ReFire
        Goto Ready
        
      Spawn:
        QDBS Z -1
        Stop
    }
}
