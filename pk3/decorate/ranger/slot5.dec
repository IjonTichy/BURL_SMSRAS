actor "Quake RL": Weapon replaces RocketLauncher
{
    Weapon.SelectionOrder 2

    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "RocketAmmo"

    +NOAUTOAIM
    +NOAUTOFIRE

    Inventory.Icon "LAUNA0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the rocket launcher"

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        QRKL A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        QRKL A 1 A_Raise
        Loop

      Ready:
        QRKL A 1 A_WeaponReady
        Loop

      // 28 tics
      Fire:
        QRKL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadFire")
        goto AfterSound

      QuadFire:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuad")
        QRKL B 0 A_FireCustomMissile("QuakeRocketQuad")
        goto Fire2

      CoopQuad:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoopQuad")
        goto Fire2

      AfterSound:
        QRKL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QRKL B 0 A_FireCustomMissile("QuakeRocket")
        goto Fire2

      CoopFire:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoop")
        goto Fire2

      Fire2:
        QRKL B 5 bright
        QRKL C 5 bright
        QRKL D 6
        QRKL E 6
        QRKL F 6
        QRKL F 0 A_Refire
        goto Ready

      Spawn:
        LAUN A -1
        Stop
    }
}


actor QuakeRocket
{
    Radius 4
    Height 4
    Speed 50

    Scale 0.65

    Projectile

    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL

    Obituary "%o rides %k's rocket"

    States
    {
      Spawn:
        DMSL A -1
        loop

      Death:
        DMSL B 0 A_Explode(64, 128, 0)
        DMSL B 0 A_Explode(64, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketQuad: QuakeRocket
{
    DamageType "NotQuadded"

    States
    {
      Death:
        DMSL B 0 A_Explode(256, 128, 0)
        DMSL B 0 A_Explode(256, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}


actor QuakeExplosion
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    States
    {
      Spawn:
        DMSL B 0 bright
        DMSL B 8 bright A_PlaySound("quakeweps/explode")
        DMSL C 6 bright
        DMSL D 4 bright
        stop
    }
}

actor QuakeRocketCoop: QuakeRocket
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (75)
}

actor QuakeRocketCoopQuad: QuakeRocketQuad
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (300)
}
