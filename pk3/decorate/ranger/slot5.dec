actor "  Rocket Launcher  ": InstantSwitchWeapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobRangeY 0.5
    //Weapon.BobSpeed 1.1
    //Weapon.BobStyle Alpha
    Weapon.SelectionOrder 2
    
    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "RocketAmmo"
    
    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 0
    Weapon.AmmoType2 "MultiRocketAmmo"

    Weapon.SisterWeapon "Spectral RL"
    
    +NOAUTOFIRE
    +UNDROPPABLE
    
    Inventory.Icon "QRKLZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the rocket launcher"
    
    States
    {
      Deselect:
        QRKL A 0 A_TakeInventory("UsingMultiRockets")
        QRKL A 0 A_TakeInventory("UsingDoEAmmo")
        goto Super::Deselect
        
      Ready:
        QRKL A 1 A_WeaponReady
        Loop
        
      MultiReady:
        QMRL A 1 A_WeaponReady
        Loop
        
      Fire:
        TNT1 A 0 ACS_ExecuteAlways(312, 0, 5, 4, 15)
        QRKL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadFire")
        goto AfterSound
        
      QuadFire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuad")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuad")
        QRKL B 0 A_FireCustomMissile("QuakeRocketQuad")
        goto Fire2
        
      CoopQuad:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoopQuad")
        goto Fire2
        
      AfterSound:
        QRKL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QRKL A 0 A_JumpIfInventory("SPModeOn", 1, "CoopFire")
        QRKL B 0 A_FireCustomMissile("QuakeRocket")
        goto Fire2
        
      CoopFire:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoop")
        goto Fire2
        
      Fire2:
        QRKL A 0 A_TakeInventory("UsingMultiRockets")
        QRKL A 0 A_TakeInventory("UsingDoEAmmo")
        QRKL BC 2 bright
        QRKL D 3 bright
        QRKL E 4
        QRKL FG 5
        QRKL H 6
        // 27 tics
        QRKL H 0 A_Refire
        goto Ready
        
      AltFire:
        TNT1 A 0 ACS_ExecuteAlways(312, 0, 7, 4, 15)
        QMRL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadAlt")
        goto AfterSoundAlt
        
      QuadAlt:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuadAlt")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuadAlt")
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket", -4, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket", -2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket",  2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket",  4)
        goto AltFire2
        
      CoopQuadAlt:
        QMRL B 0 A_FireCustomMissile("QuadClusterRkCoop", -4, 0)
        QMRL B 0 A_FireCustomMissile("QuadClusterRkCoop", -2, 0)
        QMRL B 0 A_FireCustomMissile("QuadClusterRkCoop",  2, 0)
        QMRL B 0 A_FireCustomMissile("QuadClusterRkCoop",  4)
        goto AltFire2
        
      AfterSoundAlt:
        QMRL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAlt")
        QMRL A 0 A_JumpIfInventory("SPModeOn", 1, "CoopAlt")
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket", -4, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket", -2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket",  2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRocket",  4)
        goto AltFire2
        
      CoopAlt:
        QMRL B 0 A_FireCustomMissile("QuakeClusterRkCoop", -4, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRkCoop", -2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRkCoop",  2, 0)
        QMRL B 0 A_FireCustomMissile("QuakeClusterRkCoop",  4)
        goto AltFire2
        
      AltFire2:
        QRKL A 0 A_GiveInventory("UsingMultiRockets")
        QRKL A 0 A_GiveInventory("UsingDoEAmmo")
        QMRL B 2 bright
        QMRL CD 2 bright
        QMRL E 4
        QMRL F 5
        QMRL G 5
        QMRL H 6
        // 27 tics
        QMRL H 0 A_Refire
        goto MultiReady
        
      Spawn:
        QRKL Z -1
        Stop
    }
}

actor "Spectral RL": "  Rocket Launcher  "
{
    Weapon.AmmoType ""
    Weapon.AmmoUse  0
    Weapon.AmmoGive 0

    Weapon.AmmoType2 ""
    Weapon.AmmoUse2  0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon "  Rocket Launcher  "
    +POWERED_UP

    States
    {
      Fire:
        TNT1 A 0 ACS_ExecuteAlways(312, 0, 5, 4, 15)
        TNT1 A 0 ACS_ExecuteAlways(317, 0, 20)
        QRKL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("weapons/sigil", 6)
        QRKL B 0 A_FireCustomMissile("SpectralRocket")
        goto Fire2

      AltFire:
        TNT1 A 0 ACS_ExecuteAlways(312, 0, 7, 4, 15)
        TNT1 A 0 ACS_ExecuteAlways(317, 0, 35)
        QMRL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("weapons/sigil", 6)
        QMRL B 0 A_FireCustomMissile("SpectralClusterRocket", -4, 0)
        QMRL B 0 A_FireCustomMissile("SpectralClusterRocket", -2, 0)
        QMRL B 0 A_FireCustomMissile("SpectralClusterRocket",  2, 0)
        QMRL B 0 A_FireCustomMissile("SpectralClusterRocket",  4)
        goto AltFire2
    }
}

actor UsingMultiRockets : Boolean {}

actor QuakeRocket
{
    Radius 2
    Height 2
    Speed 50
    DamageType "Explosive"
    Scale 0.3
    
    Projectile
    
    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL
    +EXPLODEONWATER
    
    Obituary "%k \csmade\c- %o \csride the rocket.\c-" // Might need to change, exact same as Doomguy's.
    
    States
    {
      Spawn:
        QRKT A -1
        loop
        
      Death:
        DMSL B 0 A_Explode(64, 128, 0)
        DMSL B 0 A_Explode(64, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketQuad: QuakeRocket
{
    Obituary "%k \cssmeared\c- %o \cswith a Quadded Rocket."
    
    DamageType "NotQuadded"
    
    States
    {
      Death:
        DMSL B 0 A_Explode(256, 128, 0)
        DMSL B 0 A_Explode(256, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketCoop: QuakeRocket
{
    +THRUSPECIES
    Species "Player"
    
    Damage (75)
}

actor QuakeRocketCoopQuad: QuakeRocketQuad
{
    +THRUSPECIES
    Species "Player"
    
    Damage (300)
}

actor QuakeClusterRocket: QuakeRocket
{
    Obituary "%k \csgave\c- %o \csfour rockets to ride.\c-"
    
    States
    {
      Death:
        DMSL B 0 A_Explode(24, 91, 0)
        DMSL B 0 A_Explode(24, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuakeClusterRkCoop: QuakeClusterRocket
{
    +THRUSPECIES
    Species "Player"
    
    Damage (38)
}

actor QuadClusterRocket: QuakeClusterRocket
{
    DamageType "NotQuadded"
    Obituary "%k \csturned\c- %o \csinto a fine mist.\c-"
    
    States
    {
      Death:
        DMSL B 0 A_Explode(96, 91, 0)
        DMSL B 0 A_Explode(96, 91)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuadClusterRkCoop: QuadClusterRocket
{
    +THRUSPECIES
    Species "Player"
    
    Damage (150)
}

actor SpectralRocket: QuakeRocketCoopQuad { +SPECTRAL }
actor SpectralClusterRocket: QuadClusterRkCoop { +SPECTRAL }
