actor "  Rocket Launcher  ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2

    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "RocketAmmo"

    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 0
    Weapon.AmmoType2 "ClusterRocketAmmo"

    +NOAUTOAIM
    +NOAUTOFIRE
    +UNDROPPABLE

    Inventory.Icon "QRKLZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the rocket launcher"

    States
    {
      Ready:
        QRKL A 1 A_WeaponReady
        Loop

      // 28 tics
      Fire:
        QRKL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadFire")
        goto AfterSound

      QuadFire:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuad")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuad")
        QRKL B 0 A_FireCustomMissile("QuakeRocketQuad")
        goto Fire2

      CoopQuad:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoopQuad")
        goto Fire2

      AfterSound:
        QRKL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QRKL A 0 A_JumpIfInventory("SPModeOn", 1, "CoopFire")
        QRKL B 0 A_FireCustomMissile("QuakeRocket")
        goto Fire2

      CoopFire:
        QRKL B 0 A_FireCustomMissile("QuakeRocketCoop")
        goto Fire2

      Fire2:
        QRKL B 5 bright
        QRKL C 5 bright
        QRKL D 6
        QRKL E 6
        QRKL F 6
        QRKL F 0 A_Refire
        goto Ready

      AltFire:
        QRKL B 0 A_PlaySound("quakeweps/rlfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadAlt")
        goto AfterSoundAlt

      QuadAlt:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuadAlt")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuadAlt")
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket", -10, 0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket", -5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket",  5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket",  10)
        goto Fire2

      CoopQuadAlt:
        QRKL B 0 A_FireCustomMissile("QuadClusterRkCoop", -10, 0)
        QRKL B 0 A_FireCustomMissile("QuadClusterRkCoop", -5,  0)
        QRKL B 0 A_FireCustomMissile("QuadClusterRkCoop",  5,  0)
        QRKL B 0 A_FireCustomMissile("QuadClusterRkCoop",  10)
        goto Fire2

      AfterSoundAlt:
        QRKL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAlt")
        QRKL A 0 A_JumpIfInventory("SPModeOn", 1, "CoopAlt")
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket", -10, 0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket", -5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket",  5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRocket",  10)
        goto Fire2

      CoopAlt:
        QRKL B 0 A_FireCustomMissile("QuakeClusterRkCoop", -10, 0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRkCoop", -5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRkCoop",  5,  0)
        QRKL B 0 A_FireCustomMissile("QuakeClusterRkCoop",  10)
        goto Fire2

      Spawn:
        QRKL Z -1
        Stop
    }
}


actor QuakeRocket: FastProjectile
{
    Radius 1
    Height 0
    Speed 50

    Scale 0.65

    Projectile

    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL

    Obituary "%k made %o ride the rocket." // Might need to change, exact same as Doomguy's.

    States
    {
      Spawn:
        DMSL A -1
        loop

      Death:
        DMSL B 0 A_Explode(64, 128, 0)
        DMSL B 0 A_Explode(64, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketQuad: QuakeRocket
{
    DamageType "NotQuadded"

    States
    {
      Death:
        DMSL B 0 A_Explode(256, 128, 0)
        DMSL B 0 A_Explode(256, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeRocketCoop: QuakeRocket
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (75)
}

actor QuakeRocketCoopQuad: QuakeRocketQuad
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (300)
}

actor QuakeClusterRocket: QuakeRocket
{
    States
    {
      Death:
        DMSL B 0 A_Explode(32, 128, 0)
        DMSL B 0 A_Explode(32, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeClusterRkCoop: QuakeClusterRocket
{
    +THRUSPECIES
    Species "Player"

    Damage (38)
}


actor QuadClusterRocket: QuakeClusterRocket
{
    DamageType "NotQuadded"

    States
    {
      Death:
        DMSL B 0 A_Explode(128, 128, 0)
        DMSL B 0 A_Explode(128, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuadClusterRkCoop: QuakeClusterRkCoop
{
    +THRUSPECIES
    Species "Player"

    Damage (150)
}
