actor "Grenade Launcher": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2

    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "RocketAmmo"

    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 0
    Weapon.AmmoType2 "ClusterRocketAmmo"

    +NOAUTOAIM
    +NOAUTOFIRE
    +UNDROPPABLE

    Inventory.Icon "QGRLZ0"

    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the grenade launcher"

    States
    {
      Ready:
        QGRL A 1 A_WeaponReady
        loop

      MultiReady:
        QMGL A 1 A_WeaponReady
        loop

      // 22 tics
      Fire:
        QGRL B 0 A_PlaySound("quakeweps/glfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadFire")
        goto AfterSound

      QuadFire:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        QGRL B 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuad")
        QGRL B 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuad")
        QGRL B 0 A_FireCustomMissile("QuakeGrenadeQuad", 0, 1, 0, 0, 0, 5)
        goto Fire2

      CoopQuad:
        QGRL B 0 A_FireCustomMissile("QuakeGrenadeCoopQuad", 0, 1, 0, 0, 0, 5)
        goto Fire2

      AfterSound:
        QGRL B 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QGRL B 0 A_JumpIfInventory("SPModeOn", 1, "CoopFire")
        QGRL B 0 A_FireCustomMissile("QuakeGrenade", 0, 1, 0, 0, 0, 5)
        goto Fire2

      CoopFire:
        QGRL B 0 A_FireCustomMissile("QuakeGrenadeCoop", 0, 1, 0, 0, 0, 5)
        goto Fire2

      Fire2:
        TNT1 A 0 A_ZoomFactor(0.98, ZOOM_INSTANT)
        QGRL B 2 bright
        TNT1 A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        QGRL C 3
        TNT1 A 0 A_ZoomFactor(1, ZOOM_INSTANT)
        QGRL D 4
        QGRL E 4
        QGRL F 3
        QGRL G 3
        QGRL H 2
        QGRL I 1
        // 22 tics
        QGRL I 0 A_Refire
        Goto Ready

      AltFire:
        QMGL B 0 A_PlaySound("quakeweps/glfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadAlt")
        goto AfterSoundAlt

      QuadAlt:
        TNT1 A 0 A_PlaySound("quakeweps/quadfire", 7)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuadAlt")
        TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "CoopQuadAlt")
        QMGL B 0 A_FireCustomMissile("QuadClusterGrenade", 0, 1, 0, 0, 0, 5)
        goto AltFire2

      CoopQuadAlt:
        QMGL B 0 A_FireCustomMissile("QuadClusterGrnCoop", 0, 1, 0, 0, 0, 5)
        goto AltFire2

      AfterSoundAlt:
        QMGL A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAlt")
        QMGL A 0 A_JumpIfInventory("SPModeOn", 1, "CoopAlt")
        QMGL B 0 A_FireCustomMissile("QuakeClusterGrenade", 0, 1, 0, 0, 0, 5)
        goto AltFire2

      CoopAlt:
        QMGL B 0 A_FireCustomMissile("QuakeClusterGrnCoop", 0, 1, 0, 0, 0, 5)
        goto AltFire2

      AltFire2:
        TNT1 A 0 A_ZoomFactor(0.98, ZOOM_INSTANT)
        QMGL B 2 bright
        TNT1 A 0 A_ZoomFactor(0.99, ZOOM_INSTANT)
        QMGL C 3
        TNT1 A 0 A_ZoomFactor(1, ZOOM_INSTANT)
        QMGL D 4
        QMGL E 4
        QMGL F 3
        QMGL G 3
        QMGL H 2
        QMGL I 1
        // 22 tics
        QMGL I 0 A_Refire
        goto MultiReady

      Spawn:
        QGRL Z -1
        Stop
    }
}

actor QuakeGrenade
{
    Radius 4
    Height 4
    Speed 24

    Projectile
    -NOGRAVITY
    DamageType "Explosive"
    Gravity 0.6
    BounceFactor 0.3

    +HEXENBOUNCE
    +RANDOMIZE
    +DEHEXPLOSION
    +GRENADETRAIL

    ReactionTime 92

    SeeSound "quakeweps/grenadebounce"

    Obituary "%k fed %o a pineapple."

    States
    {
      Spawn:
        SGRN A 0 A_JumpIf(1==1, 3)
        SGRN A 0 A_Recoil(-1)  // to stop the "freeze in midair" bug from happening
        SGRN A 0 A_PlaySound("quakeweps/blank", CHAN_VOICE)
        SGRN A 1 A_JumpIf(ACS_ExecuteWithResult(583, momx, momy, momz) < 1, "StopSpawnStart")
        SGRN A 0 A_Countdown
        loop

      StopSpawnStart:
        goto StopSpawn

      StopSpawn:
        SGRN A 1 A_Stop
        SGRN A 0 A_Countdown
        loop

      Death:
        MISL B 0 A_Explode(64, 128, 0)
        MISL B 0 A_Explode(64, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeGrenadeCoop: QuakeGrenade
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (80)
}

actor QuakeGrenadeQuad: QuakeGrenade
{
    DamageType "NotQuadded"

    States
    {
      Death:
        DMSL B 0 A_Explode(256, 128, 0)
        DMSL B 0 A_Explode(256, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeExplosion")
        stop
    }
}

actor QuakeGrenadeCoopQuad: QuakeGrenadeQuad
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (320)
}

actor QuakeClusterGrenade: QuakeGrenade
{
    Translation "80:111=48:79"
    Obituary "%k fed %o a cluster of pineapples."

    ReactionTime 46

    States
    {
      Death:
        MISL B 0 A_CustomMissile("QuakeTinyGrenade", 0,0,   0, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrenade", 0,0,  72, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrenade", 0,0, 144, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrenade", 0,0, 216, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrenade", 0,0, 288, 6, 70)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuakeClusterGrnCoop: QuakeClusterGrenade
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (32)

    States
    {
      Death:
        MISL B 0 A_CustomMissile("QuakeTinyGrnCoop", 0,0,   0, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrnCoop", 0,0,  72, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrnCoop", 0,0, 144, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrnCoop", 0,0, 216, 6, 70)
        MISL B 0 A_CustomMissile("QuakeTinyGrnCoop", 0,0, 288, 6, 70)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuadClusterGrenade: QuakeClusterGrenade
{
    DamageType "NotQuadded"

    States
    {
      Death:
        MISL B 0 A_CustomMissile("QuadTinyGrenade", 0,0,   0, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrenade", 0,0,  72, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrenade", 0,0, 144, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrenade", 0,0, 216, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrenade", 0,0, 288, 6, 70)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuadClusterGrnCoop: QuadClusterGrenade
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    DamageType "NotQuadded"
    Damage (128)

    States
    {
      Death:
        MISL B 0 A_CustomMissile("QuadTinyGrnCoop", 0,0,   0, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrnCoop", 0,0,  72, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrnCoop", 0,0, 144, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrnCoop", 0,0, 216, 6, 70)
        MISL B 0 A_CustomMissile("QuadTinyGrnCoop", 0,0, 288, 6, 70)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuakeTinyGrenade: QuakeGrenade
{
    Speed 8
    ReactionTime 46

    SeeSound "quakeweps/grenadebounce_quiet"

    Obituary "%k fed %o a cluster of pineapples."

    States
    {
      Spawn:
        SGRN A 0 A_JumpIf(1==1, 3)
        SGRN A 0 A_PlaySound("quakeweps/blank", CHAN_VOICE)
        SGRN A 0 ThrustThingZ(0, random(0, 8), random(0, 1), 1)

        SGRN A 1 A_JumpIf(ACS_ExecuteWithResult(583, momx, momy, momz) < 1, "StopSpawnStart")
        SGRN A 0 A_Jump(64, "Spawn")
        SGRN A 0 A_Countdown
        loop

      StopSpawnStart:
        goto StopSpawn

      StopSpawn:
        SGRN A 1 A_Stop
        SGRN A 0 A_Jump(64, "StopSpawn")
        SGRN A 0 A_Countdown
        loop

      Death:
        MISL B 0 A_Explode(26, 128, 0)
        MISL B 0 A_Explode(25, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuakeTinyGrnCoop: QuakeTinyGrenade
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (32)
}

actor QuadTinyGrenade: QuakeTinyGrenade
{
    DamageType "NotQuadded"

    States
    {
      Death:
        MISL B 0 A_Explode(102, 128, 0)
        MISL B 0 A_Explode(102, 128)
        TNT1 A 1 A_SpawnItemEx("QuakeTinyExplosion")
        stop
    }
}

actor QuadTinyGrnCoop: QuadTinyGrenade
{
    +THRUGHOST
    +THRUSPECIES
    Species "Player"

    Damage (128)
}
