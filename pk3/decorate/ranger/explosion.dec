actor QuakeExplosion
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    
    ReactionTime 50
    
    States
    {
      Spawn:
        TNT1 B 0 bright
        TNT1 A 0 A_CustomMissile("QuakeExplosionParticle",  0, 0, random(-180, 180), 2, random(-180, 180))
        TNT1 A 0 A_CustomMissile("QuakeExplosionParticle2", 0, 0, random(-180, 180), 2, random(-180, 180))
        TNT1 A 0 A_CustomMissile("QuakeExplosionParticle3", 0, 0, random(-180, 180), 2, random(-180, 180))
        TNT1 A 0 A_CustomMissile("QuakeExplosionParticle4", 0, 0, random(-180, 180), 2, random(-180, 180))
        TNT1 B 0 A_Countdown
        loop
        
      Death:
        TNT1 B 8 bright A_PlaySound("quakeweps/explode")
        TNT1 C 6 bright
        TNT1 D 4 bright
        stop
    }
}

actor QuakeExplosionParticle
{
    Radius 0
    Height 0

    Speed 4

    //+NOINTERACTION
    +CLIENTSIDEONLY
    //-NOINTERACTION
    +NOCLIP
    +HEXENBOUNCE

    Gravity 0.2
    BounceFactor 0.9

    Scale 0.65
    Renderstyle Add
    Alpha 0.7

    States
    {
      Spawn:
        DPFF A 0 bright
        DPFF A 0 ACS_ExecuteAlways(586, 0, 0, random(70, 130), 0)
        DPFF A 1 A_Jump(255, "SpawnLoop")
        wait

      SpawnLoop:
        DPFF A 2 bright A_FadeOut(0.05)
        loop
    }
}

actor QuakeExplosionParticle2: QuakeExplosionParticle
{
    Translation "160:167=180:187", "224:231=168:175", "64:71=36:51"

    Speed 12
    Alpha 1

    States
    {
      SpawnLoop:
        DPFF A 2 bright A_FadeOut(0.1)
        loop
    }
}

actor QuakeExplosionParticle3: QuakeExplosionParticle
{
    Speed 2
    Alpha 1

    States
    {
      SpawnLoop:
        DPFF A 2 bright A_FadeOut(0.04)
        loop
    }
}

actor QuakeExplosionParticle4: QuakeExplosionParticle
{
    Translation "160:163=220:223", "164:167=232:235", "224:231=208:215", "64:71=232:235"
    Speed 6

    States
    {
      SpawnLoop:
        DPFF A 2 bright A_FadeOut(0.04)
        loop
    }
}
