actor QuakePlayer: PlayerPawn
{
    Radius 16
    Height 56
    Mass 100
    Scale 0.19
    Speed 1
    
    Gravity 0.6
    Player.JumpZ 6.1968
    
    Health 100
    PainChance 255
    
    Species "Player"
    +THRUSPECIES
    
    Player.SoundClass "ranger"
    Player.Face "RNG"
    Player.DisplayName "Ranger"
    //Player.CrouchSprite "PLYC"
    
    Player.StartItem "QuakeClass"
    Player.StartItem "Single Shotgun"
    Player.StartItem "Clip", 50
    Player.StartItem "Axe"
    
    Player.ColorRange 112, 127
    
    States
    {
      Spawn:
        RANG A -1
        Loop

      See:
        RANG BCDE 4
        Loop

      Missile:
        RANG G 12
        Goto Spawn

      Melee:
        RANG H 6 BRIGHT
        Goto Missile
        
      Pain:
        RANG F 4
        RANG F 4 A_Pain
        Goto Spawn
        
      Pain.WolfenFlamethrower:

      Pain.MarathonPlasma:

      Pain.Slime:

      Pain.Fire:
        RANG F 4
        RANG F 4 A_PlaySound("ranger/firepain", CHAN_VOICE)
        goto Spawn
        
      Pain.Drowning:
        RANG F 4
        RANG F 4 A_PlaySound("ranger/drowning", CHAN_VOICE)
        goto Spawn
        
      Pain.Falling:
        RANG F 2
        RANG F 6 A_PlaySound("ranger/fallpain", CHAN_VOICE)
        goto Spawn
        
      Death:
        RANG I 10 A_PlayerScream
        RANG J 10 A_NoBlocking
        RANG KLM 10
        RANG N -1
        Stop
        
      Death.Drowning:
        RANG I 10 A_PlaySound("ranger/drown", CHAN_VOICE)
        RANG J 10 A_NoBlocking
        RANG KLM 10
        RANG N -1
        Stop
        
      Death.Telefrag:
        goto XDeath.Telefrag
        
      XDeath: // Need to replace with a proper gib animation
        RANG I 10 A_XScream
        RANG J 10 A_NoBlocking
        RANG KLM 10
        RANG N -1
        Stop
        
      XDeath.Telefrag: // This, too
        RANG I 10 A_PlaySound("ranger/telefragged", CHAN_VOICE)
        RANG J 10 A_NoBlocking
        RANG KLM 10
        RANG N -1
        Stop
        
      Death.ZorchPistol:
        
      Death.Zorch:
        RANG F 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        RANG F 3 A_SpawnItem("ZorchEffectSmall")
        RANG F 3 A_SetTranslucent(0.9,0)
        TNT1 A 0 A_PlayerScream
        RANG F 3 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        RANG F 3 A_SetTranslucent(0.7,0)
        RANG F 3 A_SetTranslucent(0.6,0)
        RANG F 3 A_SetTranslucent(0.5,0)
        RANG F 3 A_SetTranslucent(0.4,0)
        RANG F 3 A_SetTranslucent(0.3,0)
        RANG F 3 A_SetTranslucent(0.2,0)
        RANG F 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop
        
      Death.DukeDamage:
        
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Death
        
      Death.DukeExplosive:
        TNT1 A 0 A_JumpIfHealthLower(-5,"DukeGibStart")
        goto Death.DukeDamage
        
      DukeGibStart:
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto XDeath
        
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Ice
        
      Ice:
      Death.HexenIce:
        
      Death.Ice:
        RANG F 5 A_GenericFreezeDeath
        RANG F 1 A_FreezeDeathChunks
        Wait
    }
}

actor QuakeClass: Boolean {}

actor QuakeBullet: FastProjectile
{
    Damage (4)
    Radius 1
    Height 1
    Speed 300
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop
        
      Death:
        TNT1 A 1 A_SpawnItem("QuakeBulletPuff")
        stop
        
      XDeath:
        TNT1 A 1
        stop
    }
}

actor TelefragTester: Rocket
{
    DamageType "Telefrag"
}
