actor QuakePlayer: PlayerPawn
{
    Radius 16
    Height 56
    Mass 100

    Speed 1

    Gravity 0.75
    Player.JumpZ 6.9283

    Health 100
    GibHealth -30
    PainChance 255

    Player.DisplayName "Ranger"
    Player.CrouchSprite "PLYC"

    Player.StartItem "QuakeClass"
    Player.StartItem "Quake SG"
    Player.StartItem "Quake Fist"
    Player.StartItem "Clip", 50

    Player.ColorRange 112, 127

    States
    {
      Spawn:
        PLAY A -1
        Loop
      See:
        PLAY ABCD 4
        Loop
      Missile:
        PLAY E 12
        Goto Spawn
      Melee:
        PLAY F 6 BRIGHT
        Goto Missile
      Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        Goto Spawn
      Death:
        PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
      Death1:
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop
      XDeath:
        PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
      XDeath1:
        PLAY O 5
        PLAY P 5 A_XScream
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop
      AltSkinDeath:
        PLAY H 6
        PLAY I 6 A_PlayerScream
        PLAY JK 6
        PLAY L 6 A_NoBlocking
        PLAY MNO 6
        PLAY P -1
        Stop
      AltSkinXDeath:
        PLAY Q 5 A_PlayerScream
        PLAY R 0 A_NoBlocking
        PLAY R 5 A_SkullPop
        PLAY STUVWX 5
        PLAY Y -1
        Stop
    }
}

actor QuakeClass: Boolean {}

actor QuakeBullet: FastProjectile
{
    Damage (4)
    Radius 1
    Height 1
    Speed 300
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("DoomBulletPuff")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}
