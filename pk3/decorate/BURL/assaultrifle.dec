actor "Accurate Bullet Gun": Pistol
{
    tag "Assault Rifle"
    +weapon.noautofire
    weapon.ammotype "Clip"
    weapon.ammouse 1
    weapon.ammogive 20
    Weapon.SelectionOrder 700
    attacksound ""
    inventory.pickupmessage "Picked up an assault rifle."
    states
    {
      Spawn:
        SUBX A -1
        stop

      Ready:
        SUBM A 1 A_WeaponReady
        wait

      Select:
        SUBM AA 0 A_Raise
        SUBM A 1 A_Raise
        loop

      Deselect:
        SUBM AA 0 A_Lower
        SUBM A 1 A_Lower
        loop

      Fire:
        SUBM B 0 A_FireBullets(1.5, 1.5, 1, 20, "BURLPUFF", FBF_USEAMMO|FBF_NORANDOM)
        SUBM B 0 A_PlaySound("BURL/assaultriflefire",5)
        SUBM B 1 bright offset(0, 34) A_GunFlash
        SUBM A 1 offset(0, 34) A_GiveInventory("BURLARFired", 1) 
        SUBM A 1 offset(0, 38)
        SUBM A 0 A_JumpIfInventory("BURLARFired", 0, 2)
        SUBM A 0 offset(0, 41) A_ReFire
        SUBM A 1 offset(0, 41) A_TakeInventory("BURLARFired", 3)
        SUBM A 1 offset(0, 44)	
        SUBM A 1 offset(0, 40)
        goto Ready

      Flash:
        TNT1 A 1 A_Light1
        goto LightDone
    }
}

actor BURLARFired: Counter { Inventory.MaxAmount 3 }
