actor "Bullet Gun": Pistol
{
    tag "Pistol"

    Weapon.AmmoType ""
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0

    +NOAUTOFIRE
    +NOALERT
    attacksound ""
    states
    {
      Select:
        PISG A 0 A_JumpIfInventory("Silenced", 1, 4)
        PISG A 0 A_Raise
        PISG A 0 A_Raise
        PISG A 1 A_Raise
        loop
        SPIL A 0 A_Raise
        SPIL A 0 A_Raise
        SPIL A 1 A_Raise
        goto Select+5

      Deselect:
        PISG A 0 A_JumpIfInventory("Silenced", 1, 4)
        PISG A 0 A_Lower
        PISG A 0 A_Lower
        PISG A 1 A_Lower
        loop
        SPIL A 0 A_Lower
        SPIL A 0 A_Lower
        SPIL A 1 A_Lower
        goto Deselect+5

      Ready:
        PISG A 0 A_TakeInventory("PistolInaccuracy", 1)
        PISG A 0 A_JumpIfInventory("Silenced", 1, 2)
        PISG A 3 offset(0, 32) A_WeaponReady
        goto Ready
        SPIL A 3 A_WeaponReady
        goto Ready

      Fire:
        PISG A 0 A_JumpIfInventory("Silenced", 1, "SilencedFire")
        PISG A 0 A_JumpIfInventory("PistolInaccuracy", 2, "Fire2")
        PISG B 0 A_FireBullets(0, 0, -1, 9, "BURLPUFF", FBF_NORANDOM|FBF_USEAMMO)
        PISG B 0 A_PlayWeaponSound("BURL/pistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        PISG B 0 A_AlertMonsters
        PISG B 2 offset(0, 32) A_GunFlash
        PISG D 1 offset(0, 38) A_WeaponReady(WRF_NOBOB)
        PISG D 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        PISG D 1 offset(0, 42) A_WeaponReady(WRF_NOBOB)
        PISG D 1 offset(0, 44) A_WeaponReady(WRF_NOBOB)
        PISG E 1 offset(0, 56) A_WeaponReady(WRF_NOBOB)
        PISG E 1 offset(0, 59) A_WeaponReady(WRF_NOBOB)
        PISG E 1 offset(0, 63) A_WeaponReady(WRF_NOBOB)
        PISG E 1 offset(0, 68) A_WeaponReady(WRF_NOBOB)
        PISG E 1 offset(0, 74) A_WeaponReady(WRF_NOBOB)
        Goto Ready

      Fire2:
        PISG B 0 A_JumpIfInventory("PistolInaccuracy", 4, "Fire3")
        PISG B 0 A_FireBullets(0.65, 0.65, -1, 9, "BURLPUFF", FBF_NORANDOM|FBF_USEAMMO)
        PISG B 0 A_PlayWeaponSound("BURL/pistol")
        goto Fire+4

      Fire3:
        PISG B 0 A_JumpIfInventory("PistolInaccuracy", 6, "Fire4")
        PISG B 0 A_FireBullets(1.3, 1.3, -1, 9, "BURLPUFF", FBF_NORANDOM|FBF_USEAMMO)
        PISG B 0 A_PlayWeaponSound("BURL/pistol")
        goto Fire+4

      Fire4:
        PISG B 0 A_JumpIfInventory("PistolInaccuracy", 8, "Fire5")
        PISG B 0 A_FireBullets(2.6, 2.6, -1, 9, "BURLPUFF", FBF_NORANDOM|FBF_USEAMMO)
        PISG B 0 A_PlayWeaponSound("BURL/pistol")
        goto Fire+4

      Fire5:
        PISG B 0 A_FireBullets(5.2, 5.2, -1, 9, "BURLPUFF", FBF_NORANDOM|FBF_USEAMMO)
        PISG B 0 A_PlayWeaponSound("BURL/pistol")
        goto Fire+4

      SilencedFire:
        PISG A 0 A_JumpIfInventory("PistolInaccuracy", 2, "SFire2")
        SPIL B 0 A_FireBullets(0, 0, -1, 3, "BURLPUFF")
        SPIL B 0 A_PlayWeaponSound("BURL/silencedpistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        SPIL B 2 offset(0, 32) 
        SPIL D 1 offset(0, 38) A_WeaponReady(WRF_NOBOB)
        SPIL D 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        SPIL D 1 offset(0, 42) A_WeaponReady(WRF_NOBOB)
        SPIL D 1 offset(0, 44) A_WeaponReady(WRF_NOBOB)
        SPIL E 1 offset(0, 56) A_WeaponReady(WRF_NOBOB)
        SPIL E 1 offset(0, 59) A_WeaponReady(WRF_NOBOB)
        SPIL E 1 offset(0, 63) A_WeaponReady(WRF_NOBOB)
        SPIL E 1 offset(0, 68) A_WeaponReady(WRF_NOBOB)
        SPIL E 1 offset(0, 74) A_WeaponReady(WRF_NOBOB)
        Goto Ready

      SFire2:
        PISG A 0 A_JumpIfInventory("PistolInaccuracy", 4, "SFire3")
        SPIL B 0 A_FireBullets(0.65, 0.65, -1, 3, "BURLPUFF")
        SPIL B 0 A_PlayWeaponSound("BURL/silencedpistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        goto SilencedFire+4

      SFire3:
        PISG A 0 A_JumpIfInventory("PistolInaccuracy", 6, "SFire4")
        SPIL B 0 A_FireBullets(1.3, 1.3, -1, 3, "BURLPUFF")
        SPIL B 0 A_PlayWeaponSound("BURL/silencedpistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        goto SilencedFire+4

      SFire4:
        PISG A 0 A_JumpIfInventory("PistolInaccuracy", 8, "SFire5")
        SPIL B 0 A_FireBullets(2.6, 2.6, -1, 3, "BURLPUFF")
        SPIL B 0 A_PlayWeaponSound("BURL/silencedpistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        goto SilencedFire+4

      SFire5:
        SPIL B 0 A_FireBullets(5.2, 5.2, -1, 3, "BURLPUFF")
        SPIL B 0 A_PlayWeaponSound("BURL/silencedpistol")
        PISG A 0 A_GiveInventory("PistolInaccuracy", 1)
        goto SilencedFire+4

      AltFire:
        PISG A 0 A_JumpIfInventory("Silencer", 1, 1)
        goto Ready
        PISG A 0 A_JumpIfInventory("Silenced", 1, "AltFire2")
        PISG AAAAAAAAAAAAA 1 A_Lower
        PISG A 5 A_GiveInventory("Silenced", 1)
        SPIL AAAAAAAAAAAAA 1 A_Raise
        goto Ready

      AltFire2:
        SPIL AAAAAAAAAAAAA 1 A_Lower
        PISG A 5 A_TakeInventory("Silenced", 9)
        PISG AAAAAAAAAAAAA 1 A_Raise
        goto Ready

      Flash:
        PISF A 1 bright offset(0, 32) A_Light1
        goto LightDone
    }
}

actor SilencedPistol : CustomInventory
{
    inventory.pickupmessage "Picked up a silencer. Equip with altfire."
    states
    {
      Spawn:
        SPIP A -1
        stop

      Pickup:
        TNT1 A 0 A_GiveInventory("Silencer", 1)
        stop
    }
}

actor Silenced : Boolean {}
actor Silencer : Boolean {}

actor PistolInaccuracy : Ammo
{
    inventory.amount 1
    inventory.maxamount 10
    ammo.backpackamount 0
    ammo.backpackmaxamount 10
    +IGNORESKILL
}
