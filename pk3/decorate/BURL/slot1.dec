actor "Eager Beaver": Chainsaw
{
    obituary "%o couldn't escape %k's chainsaw."
    inventory.pickupmessage "Chainsaw! The great communicator!"
    weapon.slotnumber 1
    states
    {
      Fire:
        DSAW A 0 A_Saw
        DSAW A 3 A_FireCustomMissile("SawRipper")
        DSAW B 0 A_Saw
        DSAW B 3 A_FireCustomMissile("SawRipper")
        DSAW B 0 A_ReFire
        Goto Ready

      AltFire:
        DSAW A 1 offset(0, 36)
        DSAW A 1 offset(0, 40)
        DSAW A 1 offset(0, 44)
        DSAW A 1 offset(0, 48)
        DSAW A 1 offset(0, 52)
        DSAW A 1 offset(0, 56)
        DSAW A 0 A_ReFire
        DSAW A 1 offset(0, 48)
        DSAW A 1 offset(0, 40)
        DSAW A 1 offset(0, 32)
        goto Ready

      AltHold:
        DSAW A 0 A_PlaySound("doomguy/sawidle",CHAN_AUTO,0.666)
        DSAW A 1 offset(0, 56)
        DSAW B 1 offset(0, 56)
        DSAW A 0 A_PlaySound("doomguy/sawidle",1)
        DSAW A 0 A_PlaySound("doomguy/sawidle",5,0.4)
        DSAW A 0 A_ReFire
        DSAW A 1 offset(0, 48)
        DSAW A 1 offset(0, 40)
        DSAW A 1 offset(0, 32)
        DSAW A 0 A_FireCustomMissile("TossedChainsaw")
        DSAW A 0 A_TakeInventory("Eager Beaver", 1)
        goto Ready

      Spawn:
        WSAW A -1
        stop
    }
}

actor SawRipper
{
    obituary "%o couldn't escape %k's chainsaw."
    damage (5)
    +ripper
    speed 65
    radius 1
    height 1
    projectile
    states
    {
      Spawn:
        TNT1 A 1
        stop
    }
}

actor TossedChainsaw : SawRipper
{
    damage (35)
    speed 32
    radius 16
    height 28
    seesound "doomguy/sawfull"
    -NOGRAVITY
    scale 0.3
    +EXTREMEDEATH
    +SKYEXPLODE
    obituary "%o couldn't outrun %k's chainsaw."
    gravity 0.125

    Species "Player"
    +THRUSPECIES

    states
    {
      Spawn:
        SAWW A 1 A_LoopActiveSound
        loop

      Death:
        TNT1 A 0 A_SpawnItem("Eager Beaver")
        stop
    }
}
