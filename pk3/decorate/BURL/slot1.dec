actor "Eager Beaver": Chainsaw
{
    obituary "%o couldn't escape %k's chainsaw."
    inventory.pickupmessage "Chainsaw! The great communicator!"
    weapon.slotnumber 1
    states
    {
      Fire:
        SAWG A 0 A_Saw
        SAWG A 3 A_FireCustomMissile("SawRipper")
        SAWG B 0 A_Saw
        SAWG B 3 A_FireCustomMissile("SawRipper")
        SAWG B 0 A_ReFire
        Goto Ready

      AltFire:
        SAWG A 1 offset(0, 36)
        SAWG A 1 offset(0, 40)
        SAWG A 1 offset(0, 44)
        SAWG A 1 offset(0, 48)
        SAWG A 1 offset(0, 52)
        SAWG A 1 offset(0, 56)
        SAWG A 0 A_ReFire
        SAWG A 1 offset(0, 48)
        SAWG A 1 offset(0, 40)
        SAWG A 1 offset(0, 32)
        goto Ready

      AltHold:
        SAWG A 0 A_PlaySound("doomguy/sawidle",CHAN_AUTO,0.666)
        SAWG A 1 offset(0, 56)
        SAWG B 1 offset(0, 56)
        SAWG A 0 A_PlaySound("doomguy/sawidle",1)
        SAWG A 0 A_PlaySound("doomguy/sawidle",5,0.4)
        SAWG A 0 A_ReFire
        SAWG A 1 offset(0, 48)
        SAWG A 1 offset(0, 40)
        SAWG A 1 offset(0, 32)
        SAWG A 0 A_FireCustomMissile("TossedChainsaw")
        SAWG A 0 A_TakeInventory("Eager Beaver", 1)
        goto Ready
    }
}

actor SawRipper
{
    obituary "%o couldn't escape %k's chainsaw."
    damage (5)
    +ripper
    speed 65
    radius 1
    height 1
    projectile
    states
    {
      Spawn:
        TNT1 A 1
        stop
    }
}

actor TossedChainsaw : SawRipper
{
    damage (35)
    speed 32
    radius 16
    height 28
    seesound "doomguy/sawfull"
    -NOGRAVITY
    scale 0.3
    +EXTREMEDEATH
    +SKYEXPLODE
    obituary "%o couldn't outrun %k's chainsaw."
    gravity 0.125
    states
    {
      Spawn:
        SAWW A 1 A_LoopActiveSound
        loop

      Death:
        TNT1 A 0 A_SpawnItem("Eager Beaver")
        stop
    }
}
