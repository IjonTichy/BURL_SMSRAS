actor "It shoots green": BFG9000
{
    -WEAPON.NOAUTOFIRE
    -WEAPON.NOAUTOAIM
    +UNDROPPABLE
    States
    {
      Select:
        DBFG A 1 A_Raise
        loop

      Deselect:
        DBFG A 1 A_Lower
        loop
        
      Ready:
        DBFG A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerBAHZERKER",1,"FireBERSERK")
        DBFG A 20 A_BFGSound
        DBFG B 10 A_GunFlash
        DBFG B 10 A_FireCustomMissile("BFGBowl")
        DBFG B 20 A_ReFire
        Goto Ready
        
      FireBERSERK:
        DBFG A 16 A_BFGSound
        DBFG B 8 A_GunFlash
        DBFG B 8 A_FireCustomMissile("BFGBowl")
        DBFG B 16 A_ReFire
        Goto Ready

      Spawn:
        WBFG A -1
        stop
    }
}


actor BFGBowl : BFGBall replaces BFGBall
{
    Speed 40
    Damage (200*random(2,4))
    -RANDOMIZE

    Species "Player"
    +THRUSPECIES

    States
    {
        
      Spawn:
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",54,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",30,14,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",30,-14,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS A 1 Bright
        DBFS A 0 Bright A_SpawnItemEx("BFGTrail1",0,0,0)
        DBFS A 0 Bright A_SpawnItemEx("BFGTrail2",-4,0,0)
        DBFS A 1 Bright
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",54,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",30,14,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS A 0 Bright A_SpawnItemEx("BFGBLOWl",30,-14,0,0,0,0,0,SXF_NOCHECKPOSITION)
        DBFS B 1 Bright
        DBFS B 0 Bright A_SpawnItemEx("BFGTrail1",0,0,0)
        DBFS B 0 Bright A_SpawnItemEx("BFGTrail2",-4,0,0)
        DBFS B 1 Bright
        Loop
        
      Death:
        DBFE AB 8 Bright
        DBFE C 8 Bright A_BFGSpray("EmptyBFGSpray",80,10)
        DBFE DEF 8 Bright
        Stop
    }
}

actor EmptyBFGSpray
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        TNT1 A 0
        stop
    }
}

actor BFGBLOWl
{
    Radius 1
    Height 1
    +NOINTERACTION
    +EXTREMEDEATH
    DamageType "FECK9000"

    States
    {
        
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(50,50,0)
        TNT1 A 2
        Stop
    }
}

actor BFGTrail1
{
    Speed 25
    Radius 1
    Height 1
    Scale 0.6
    PROJECTILE
    +NOINTERACTION
    +CLIENTSIDEONLY
    Renderstyle Add
    Alpha 0.66

    States
    {
        
      Spawn:
        DBFS A 0 A_FadeOut(0.02)
        DBFS AB 1 Bright A_BishopMissileWeave
        Loop
        
      Death:
        "----" A 1 A_FadeOut(0.1)
        Wait
    }
}

actor BFGTrail2 : BFGTrail1
{
    Scale 0.5
    Speed 5
    Alpha 0.5

    States
    {
      Spawn:
        DBFS AA 1 Bright A_FadeOut(0.02)
        Loop
    }
}
