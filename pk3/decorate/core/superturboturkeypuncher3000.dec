actor ChickenKillCount : Counter { Inventory.MaxAmount 999 }
actor ChickenModeOn : Boolean {}

Actor SuperCoopChicken
{
    Health 65
    Radius 32
    Height 32
    Mass 40
    Speed 15
    PainChance 200
	MeleeRange 64
	Gravity 0.1
    Monster
    -COUNTKILL
    +WINDTHRUST
    +DONTMORPH
    +FLOORCLIP
	+THRUSPECIES
	DropItem "HealPack0", 64
	DropItem "ArmorScrap", 64
	DropItem "HealPack0", 32
	DropItem "ArmorScrap", 32
	DropItem "HealPack1", 32
	DropItem "HealPack1", 16
	DropItem "ArmorScrap", 16
	DropItem "Ammo1Small", 16
	DropItem "Ammo2Small", 16
	DropItem "Ammo3Small", 16
	DropItem "Ammo4Small", 16
	DropItem "ArmorScrap", 8
	Species "Chicken"
    SeeSound "chicken/pain"
    AttackSound "chicken/attack"
    PainSound "chicken/pain"
    DeathSound "chicken/death"
    ActiveSound "chicken/active"
    Obituary "%o was pecked to death by a chicken."
    States
    {
    Spawn:
        CHKN AB 5 A_Look
        goto See
		
    Idle:
	    TNT1 A 0 A_Look
        CHKN AB 3 A_Wander
        Loop
		
    See:
        CHKN AB 2 A_Chase
        CHKN AB 2 A_Chase
        CHKN AB 2 A_FastChase
        Loop
		
    Pain:
	    TNT1 AAAAA 0 A_Feathers
        CHKN D 5 A_Feathers
        CHKN C 5 A_Pain
        Goto See
		
    Melee:
        CHKN A 3 A_FaceTarget
        CHKN C 3 A_CustomMeleeAttack(random(3,5))
        Goto See
		
      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        CHKN E 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"Death")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
    Death:
        TNT1 A 0 A_GiveToTarget("ChickenKillCount",1)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CHKN E 6 A_Scream
	    TNT1 AAAAA 0 A_Feathers
        CHKN F 6 A_Feathers
        CHKN G 6
        CHKN H 6 A_NoBlocking
        CHKN IJK 6
        CHKN L -1
        Stop

    Death.ZorchPistol:

    Death.Zorch:
    Death.ZorchKamikaze:
    Death.PhasingZorch:
    Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("ChickenKillCount",1)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CHKN D 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorchsmall","soundslot5",0,0)
        CHKN D 3 A_SpawnItem("ZorchEffectSuperTiny")
        CHKN D 3 A_SetTranslucent(0.9,0)
        CHKN DD 0 A_Feathers
        CHKN D 0 A_Scream
        CHKN D 3 A_SetTranslucent(0.8,0)
        TNT1 D 0 A_NoBlocking
        CHKN D 3 A_SetTranslucent(0.7,0)
        CHKN D 3 A_SetTranslucent(0.6,0)
        CHKN D 3 A_SetTranslucent(0.5,0)
        CHKN D 3 A_SetTranslucent(0.4,0)
        CHKN D 3 A_SetTranslucent(0.3,0)
        CHKN D 3 A_SetTranslucent(0.2,0)
        CHKN D 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

    Ice:
    Freeze:
    Death.Ice:
        TNT1 A 0 A_GiveToTarget("ChickenKillCount",1)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        CHKN D 0 A_UnsetSolid
        CHKN D 70 A_GenericFreezeDeath
        CHKN D 0 A_IceGuyDie
        CHKN D 1 A_IceGuyDie
        TNT1 A -1
        stop
    }
}

actor SuperDMChicken : SuperCoopChicken
{
    Health 50
    Radius 24
    Height 24
    DropItem "NoArmor"
	+FRIGHTENED
}

actor ChickodemonBall : CacodemonBall
{
    Scale 0.5
    Speed 50
    DamageType "DoomCacodemon"
    +THRUGHOST
}

actor AssaultChicken : SuperCoopChicken
{
    Scale 1.5
    Health 65
    +FLOAT
    +NOGRAVITY
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "80:111=16:47", "3:3=42:42"
    Speed 20
	Obituary "%o was shot down by an assault chicken."
	Species "Chicken"
	DropItem "BulletPuff"
	States
	{
    Missile:
        CHKN A 2 A_FaceTarget
        CHKN C 2 A_CustomMissile("ChickodemonBall")
        Goto See
		
	Death:
	    TNT1 A 0 A_Gravity
        TNT1 A 0 A_GiveToTarget("ChickenKillCount",1)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CHKN E 6 A_Scream
	    TNT1 AAAAA 0 A_Feathers
        CHKN F 6 A_Feathers
        CHKN G 6
        CHKN H 6 A_NoBlocking
        CHKN IJK 6
        CHKN L -1
        Stop

    Ice:
    Freeze:
    Death.Ice:
	    TNT1 A 0 A_Gravity
        TNT1 A 0 A_GiveToTarget("ChickenKillCount",1)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        CHKN D 0 A_UnsetSolid
        CHKN D 70 A_GenericFreezeDeath
        CHKN D 0 A_IceGuyDie
        CHKN D 1 A_IceGuyDie
        TNT1 A -1
        stop
	}
}

actor ExplosiveChicken : SuperCoopChicken
{
    Health 50
    Scale 1.75
	Speed 30
	Translation "80:103=5:8", "104:111=0:0", "3:3=8:8"
	MeleeRange 128
	Obituary "%o was blown up by an explosive chicken."
	+THRUSPECIES
    +NOTARGET
	Species "Chicken"
	DropItem "BulletPuff"
	States
	{
	Melee:
        CHKN A 3 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("DoomguyDiesWhenHeIsKilled",0,0,16,0,0,0,0,32)
        CHKN E 1 A_SpawnItemEx("ChickenExplode",0,0,16,0,0,0,0,32)
		CHKN E 1
		goto Death
    Death:
	    TNT1 A 0
	    TNT1 A 0 A_NoBlocking
        CHKN E 3 A_Scream
	    TNT1 AAAAA 0 A_Feathers
        CHKN F 6 A_Feathers
        CHKN G 6
        CHKN H 6
        CHKN IJK 6
        CHKN L -1
        Stop
	}
}

actor ChickenExplode
{
    Radius 1
    Height 1
	Speed 0
    +NOCLIP
	+FORCERADIUSDMG
    -SOLID
	Obituary "%o was blown up by an explosive chicken."
    States
    {
      Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(128,192,0)
        Stop
    }
}